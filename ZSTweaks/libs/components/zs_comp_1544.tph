ACTION_IF (VARIABLE_IS_SET ~zs_comp_1544~) BEGIN
  FAIL ~Script already included: zs_comp_1544~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1544~ = 1
END

WITH_SCOPE BEGIN
    DEFINE_PATCH_MACRO add_spell_headers BEGIN
        SET type = header_type
        SET target = tgt
        SET range = 5000
        SET required_level = index + 1
        SET type=1
        SET location=4
        SET target=1
        SET target_count=0
        SET speed=0
        SET projectile=1
        SET zs_min_level_for_copy = 0
        SET copy_header=0
        SET insert_point=~-1~
        SPRINT icon ~~
        LPM ADD_SPELL_HEADER
        SET parameter1= index+1
        SET header=index+1
        SET opcode = op
        SET target = op_target
        SET timing = 0
        SET parameter2 = 0
        SET resist_dispel = 0
        SET duration = 0
        SET probability1 = 100
        SET probability2 = 0
        SET power = 0
        SET dicenumber = 0
        SET dicesize = 0
        SET savingthrow = 0
        SET savebonus = 0
        SET special = 0
        SET insert_point = "-1"
        SPRINT resource "%spell%"
        LPM ADD_SPELL_EFFECT
    END

    PRINT "Creating subspells to allow scrolls to scale with character level..."
    SILENT
    OUTER_SET scrolls = 0
    OUTER_SPRINT prefix "rpsc"
    COPY_EXISTING_REGEXP ~^.+\.itm$~ ~override~
        PATCH_IF SHORT_AT(0x1c) = 11 AND BYTE_AT(0x2a) != 0 BEGIN
            READ_LONG 0x0c scroll_name
            PATCH_IF scrolls > 9999 BEGIN
                PATCH_VERBOSE
                PATCH_FAIL "Too many scrolls to process!"
            END ELSE PATCH_IF scrolls < 10 BEGIN
                SPRINT number "000%scrolls%"
            END ELSE PATCH_IF scrolls < 100 BEGIN
                SPRINT number "00%scrolls%"
            END ELSE PATCH_IF scrolls < 1000 BEGIN
                SPRINT number "0%scrolls%"
            END ELSE PATCH_IF scrolls < 10000 BEGIN
                SPRINT number "%scrolls%"
            END

            SET found = 0
            GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x38
            PHP_EACH ab_array AS int => ab_off BEGIN
                GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
                PHP_EACH fx_array AS int => fx_off BEGIN
                    SET op = SHORT_AT ( fx_off )
                    SET op_target = BYTE_AT ( fx_off + 0x2 )
                    PATCH_IF op = 146 OR op = 148 BEGIN
                        ++found
                        PATCH_IF found = 1 BEGIN
                            READ_ASCII ( fx_off + 0x14 ) spell
                            INNER_PATCH_FILE ~%spell%.spl~ BEGIN
                                READ_LONG 0x64 offset
                                SET header_type = BYTE_AT ( offset )
                                SET tgt = BYTE_AT ( offset + 0xc )
                            END

                            INNER_ACTION BEGIN
                                OUTER_SET flags = 33604096
                                OUTER_SET type = 4
                                OUTER_SET school = 0
                                OUTER_SET sectype = 0
                                OUTER_SET level = 1
                                OUTER_SPRINT spell ~%prefix%%number%~
                                LAM CREATE_SPELL 
                                COPY_EXISTING ~%prefix%%number%.spl~ ~override~
                                    FOR ( index=0; index < 50; ++index ) BEGIN
                                        LPM add_spell_headers
                                    END
                            END

                            ++scrolls
                            LPF ALTER_EFFECT INT_VAR match_opcode=op parameter2=1 resist_dispel=0 power=0 STR_VAR resource=~%prefix%%number%~ END
                        END
                    END
                END
            END
        END
    BUT_ONLY
    VERBOSE
    PRINT "Found %scrolls% usable scrolls for scroll scaling."
END
