ACTION_IF (VARIABLE_IS_SET ~zs_comp_230~) BEGIN
  FAIL ~Script already included: zs_comp_230~
END ELSE BEGIN
  OUTER_SET ~zs_comp_230~ = 1
END

ACTION_IF NOT is_iwdee BEGIN

    COPY_EXISTING ~SPPR416.SPL~ ~override~
        LPF SPL_REF_NAME RET cloak_fear_strref = result END

    COPY ~%MOD_FOLDER%/spells/cleric/cloak_fear~ ~override~
    COPY_EXISTING ~SPPR416.SPL~ ~override~
        LPF ALTER_SPELL INT_VAR ref_name=cloak_fear_strref ref_desc=RESOLVE_STR_REF(@6640) END
        LPF ALTER_EFFECT INT_VAR match_opcode = 215 STR_VAR resource = ~ICCLKFR2~ END

    COPY_EXISTING ~#COFEAR.SPL~ ~override~
        PATCH_IF is_bgee BEGIN
            LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = 25818 END
        END

        PATCH_IF is_bg2ee_or_eet BEGIN
            LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = 14007 END
        END
        LPF DELETE_EFFECT INT_VAR match_opcode = 206 END

    // ADD EFFECT REMOVAL TO AURA EFFECTS TO VARIOUS REMOVE-FEAR TYPE EFFECTS only affects spells for now
    COPY_EXISTING_REGEXP ~^.+\.spl$~ ~override~
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=321 STR_VAR match_resource=~SPPR416~ resource=~#COFEAR~ END
    BUT_ONLY

END ELSE BEGIN

    COPY_EXISTING ~#COFEAR.SPL~ ~override~
        LPF DELETE_EFFECT INT_VAR match_opcode = 206 END
        
END
