ACTION_IF (VARIABLE_IS_SET ~zs_comp_1546~) BEGIN
  FAIL ~Script already included: zs_comp_1546~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1546~ = 1
END

ACTION_IF zst_1546_buckler_tweak BEGIN
    COPY "%MOD_FOLDER%/items/bucklers" "override"

    OUTER_SET projectile = 345
    ACTION_IF is_bgee BEGIN
        OUTER_SET projectile = 364
    END ELSE ACTION_IF is_iwdee BEGIN
        OUTER_SET projectile = 481
    END

    ACTION_FOR_EACH level IN 0 1 2 3 4 5 6 BEGIN
        COPY_EXISTING ~ZSTWBK#%level%.spl~ ~override~
            LPF ALTER_HEADER INT_VAR projectile = %projectile% END
    END

    ACTION_FOR_EACH level IN 0 1 2 3 4 5 6 BEGIN
        COPY_EXISTING ~ZSTWBK0%level%.spl~ ~override~
            LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@8590) END
    END

    COPY_EXISTING ~ZSTWBK1A.spl~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode = 318 parameter2 = zs_slashing_immunity END
    COPY_EXISTING ~ZSTWBK1B.spl~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode = 318 parameter2 = zs_crushing_immunity END
    COPY_EXISTING ~ZSTWBK1C.spl~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode = 318 parameter2 = zs_piercing_immunity END
    COPY_EXISTING ~ZSTWBK1D.spl~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode = 318 parameter2 = zs_missile_immunity  END
END

COPY_EXISTING ~SHLD08.ITM~ ~override~
    READ_LONG 0x8 buckler_strref

PATCH_IF zst_1546_tower_tweak BEGIN
    COPY_EXISTING "SHLD05.ITM" ~override~
        SPRINT hyphen @99991
        SPRINT ac_string @99992
        READ_STRREF UNIDENTIFIED_DESC desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%hyphen%[ %TAB%]*%ac_string%[ %TAB%]*:?[ %TAB%]*\)\+1~ ~\1+2~
        END
        SAY_EVALUATED UNIDENTIFIED_DESC ~%desc%~
        LPF ITM_REF_UDESC RET changed_description_index = result END
END

COPY_EXISTING_REGEXP ~^.+\.itm$~ ~override~
    LPF ITM_CAT RET cat = result END
    LPF ITM_APPEARANCE RET appearance = result END
    PATCH_IF cat = 12 AND NOT ("%appearance%" STR_EQ "  ") BEGIN // shields
        PATCH_IF zst_1546_everyone_can_use_all_shields BEGIN
            WRITE_LONG 0x1e 0
        END

        READ_LONG 0x8 shield_strref
        PATCH_IF shield_strref = large_shield_strref BEGIN

            PATCH_IF zst_1546_tower_tweak BEGIN
                LPF GET_SHIELD_LEVEL RET result END
                SET base_ac = result + 1
                SET new_ac = result + 2
                LPF ALTER_EFFECT INT_VAR match_opcode=0 match_parameter2=0 match_parameter1=base_ac parameter1=new_ac END
                LPF ADD_EQUIPPED_EFF INT_VAR p1="-1" STR_VAR effects = ~op=278;op=36;op=190,p1=-2~ END
                PATCH_IF zst_1546_speed_debuffs BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=176,p1=80,p2=5~ END
                END

                PATCH_IF NOT "%SOURCE_RES%" STR_EQ "SHLD05" BEGIN
                    LPF ALTER_ITEM INT_VAR unid_ref_desc = changed_description_index END
                    SPRINT hyphens @99991
                    SPRINT ac_string @99992
                    LPF ITM_REF_DESC RET description_index = result END
                    PATCH_IF description_index > "-1" BEGIN
                        READ_STRREF 0x54 desc
                        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%hyphen%[ %TAB%]*%ac_string%[ %TAB%]*:?[ %TAB%]*\)\+%base_ac%~ ~\1\+%new_ac%~
                        END
                        SAY_EVALUATED 0x54 ~%desc%~
                    END
                END
            END

            PATCH_IF zst_1546_everyone_can_use_all_shields = 0 BEGIN
                PATCH_IF zst_1546_tower_single_classed_fighter_classes BEGIN
                    WRITE_LONG 0x1e 1615853504
                END ELSE PATCH_IF zst_1546_tower_all_fighter_classes BEGIN
                    WRITE_LONG 0x1e 1615594432
                END
            END
        END

        PATCH_IF shield_strref = medium_shield_strref BEGIN
            PATCH_IF zst_1546_medium_tweak BEGIN
                LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=36,p1=-1;op=190,p1=-1~ END
                PATCH_IF zst_1546_speed_debuffs BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=176,p1=90,p2=5~ END
                END
            END

            PATCH_IF zst_1546_everyone_can_use_all_shields = 0 BEGIN
                PATCH_IF zst_1546_small_medium_thieves_bards BEGIN
                    WRITE_LONG 0x1e 1610874880
                END
            END
        END

        PATCH_IF shield_strref = small_shield_strref BEGIN

            PATCH_IF zst_1546_small_tweak BEGIN
                LPF PATCH_SHIELD INT_VAR type=2 END
            END

            PATCH_IF zst_1546_everyone_can_use_all_shields = 0  BEGIN
                PATCH_IF zst_1546_small_thieves_bards BEGIN
                    WRITE_LONG 0x1e 1610874880
                END
            END
        END

        PATCH_IF zst_1546_buckler_tweak AND shield_strref = buckler_shield_strref BEGIN
            LPF PATCH_SHIELD INT_VAR type=4 END
            LPF GET_SHIELD_LEVEL RET result END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=232,p1=1,p2=7,res=ZSTWBK#%result%~ END
            PATCH_IF zst_1546_everyone_can_use_all_shields = 0 BEGIN
                PATCH_IF zst_1546_buckler_wizards BEGIN
                    WRITE_LONG 0x1e 536870912
                END
            END
        END
    END
BUT_ONLY

// patch weapon enchantment protections with protection vs buckler parry
ACTION_IF zst_1546_buckler_tweak BEGIN
    PRINT "Patching creatures, spells, and items with weapon immunities with appropriate buckler parry immunity..."
    COPY_EXISTING_REGEXP ~^.+\.\(cre\|itm\|spl\)$~ ~override~
        LPF PATCH_FOR_BUCKLER_IMMUNITIES END
    BUT_ONLY
END
