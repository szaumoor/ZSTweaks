ACTION_IF (VARIABLE_IS_SET ~zs_comp_1410~) BEGIN
  FAIL ~Script already included: zs_comp_1410~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1410~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    ACTION_IF zst_1410_ureaver_dispel OR zst_1410_ureaver_dicesize BEGIN
        
        INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
        COPY_EXISTING ~dam5.eff~ ~override~
            LPF ALTER_EFF_FILE INT_VAR special=1024 END
        OUTER_SET two_handed_comp_installed = zst_1150_dice_twohanded_swords AND 
                                             (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR
                                             (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND
                                             zst_group_1150_weapon_dice)) 

    END ELSE BEGIN

        WARN ~No Unholy Reaver tweaks were selected~

    END

    ACTION_IF zst_1410_ureaver_dispel BEGIN

        COPY_EXISTING ~ohreaver.itm~ ~override~
            LPF ALTER_EFFECT INT_VAR check_globals=0 probability1=100 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 savingthrow=1 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode=177 match_parameter2=8 match_parameter1=1 savingthrow=0 END

            PATCH_IF zst_1410_ureaver_dicesize = 0 BEGIN

                SPRINT strref @6281
                PATCH_IF two_handed_comp_installed BEGIN
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                    END
                    SAY_EVALUATED 0x54 ~%strref%~
                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6281) END
                END

            END

    END

    ACTION_IF zst_1410_ureaver_dicesize BEGIN

        COPY_EXISTING "ohreaver.itm" ~override~
            PATCH_IF zst_1410_ureaver_dispel BEGIN
                SPRINT strref @6282
            END ELSE BEGIN
                SPRINT strref @6280
            END

            PATCH_IF two_handed_comp_installed BEGIN

                LPF ALTER_HEADER INT_VAR match_type=1 match_dicesize=9 match_dicenumber=1 match_damage=6 dicesize=6 dicenumber=2 damage=5 END
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END
                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]12[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\12d6+%dmg%~
                END
                SAY_EVALUATED 0x54 ~%strref%~

            END ELSE BEGIN

                LPF ALTER_HEADER INT_VAR match_type = 1 match_dicesize = 10 dicesize = 12 END

                PATCH_IF zst_1410_ureaver_dispel BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6282) END
                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6280) END
                END

            END
    END

END
