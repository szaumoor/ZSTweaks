ACTION_IF is_bg2ee_or_eet BEGIN


    ACTION_IF zst_1591_soul_reaver_nerf OR zst_1591_soul_reaver_dicesize BEGIN

        INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
            OUTER_SET two_handed_comp_installed = zst_1150_dice_twohanded_swords AND
                                                (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR 
                                                (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND zst_group_1150_weapon_dice))

        ACTION_IF zst_1591_soul_reaver_nerf BEGIN

            COPY_EXISTING "sw2h08.itm" ~override~
                SPRINT strref @6830
                LPF ALTER_EFFECT INT_VAR match_opcode=54 match_duration=120 duration=30 savingthrow=1 savebonus="-4" END

                PATCH_IF two_handed_comp_installed BEGIN
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                    END
                    SAY_EVALUATED 0x54 ~%strref%~
                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6830) END
                END

            COPY_EXISTING "c2sw2h02.itm" ~override~
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@50130) END
                LPF ALTER_EFFECT INT_VAR match_opcode=54 duration=30 savingthrow=1 savebonus="-5" END
                LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicesize = 3 special = 1024 END
                LPF ALTER_EFFECT_EX INT_VAR savingthrow = 1 savebonus = "-2" STR_VAR match_opcode_ex="(o_opcode != 12 AND o_opcode != 54)" END
            BUT_ONLY IF_EXISTS

        END

        ACTION_IF zst_1591_soul_reaver_dicesize BEGIN

            COPY_EXISTING "sw2h08.itm" ~override~
                PATCH_IF zst_1591_soul_reaver_nerf BEGIN
                    SPRINT strref @6850
                END ELSE BEGIN
                    SPRINT strref @6840
                END

                PATCH_IF two_handed_comp_installed BEGIN
                    LPF ALTER_HEADER INT_VAR match_dicesize = 9 match_damage = 5 dicesize = 6 dicenumber = 2 damage = 4 END
                END ELSE BEGIN
                    LPF ALTER_HEADER INT_VAR match_dicesize = 10 dicesize = 12 END
                END

                PATCH_IF two_handed_comp_installed BEGIN
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]12[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\12d6+%dmg%~
                    END
                    SAY_EVALUATED 0x54 ~%strref%~
                END ELSE BEGIN
                    PATCH_IF zst_1591_soul_reaver_nerf BEGIN
                        LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6850) END
                    END ELSE BEGIN
                        LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6840) END
                    END
                END

            COPY_EXISTING "c2sw2h02.itm" ~override~
                PATCH_IF two_handed_comp_installed BEGIN
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]12[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\12d6+%dmg%~
                    END
                    SAY_EVALUATED 0x54 ~%strref%~
                END
            BUT_ONLY IF_EXISTS

        END
                                                    
    END ELSE BEGIN WARN ~[WARN]: No tweaks were selected for Soul Reaver~ END

END
