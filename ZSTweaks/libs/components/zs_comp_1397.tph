ACTION_IF (VARIABLE_IS_SET ~zs_comp_1397~) BEGIN
  FAIL ~Script already included: zs_comp_1397~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1397~ = 1
END

COPY ~%SOURCE_RES%/general/ranged/sneak~ ~override~

ACTION_CLEAR_ARRAY thief_clabs
ACTION_CLEAR_ARRAY ranger_clabs
ACTION_CLEAR_ARRAY monk_clabs

ACTION_DEFINE_ASSOCIATIVE_ARRAY thief_clabs  BEGIN clabth01, 0 => 1 END
ACTION_DEFINE_ASSOCIATIVE_ARRAY ranger_clabs BEGIN clabra01, 0 => 1 END
ACTION_DEFINE_ASSOCIATIVE_ARRAY monk_clabs   BEGIN clabmo01, 0 => 1 END
  
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR ( index = 2 ; index < rows ; ++index ) BEGIN
    
    READ_2DA_ENTRY index 8 9 class

    PATCH_IF class = class_thief BEGIN
      READ_2DA_ENTRY index 5 9 clab
      DEFINE_ASSOCIATIVE_ARRAY thief_clabs BEGIN "%clab%" => 0 END
    END  

    PATCH_IF class = class_ranger BEGIN
      READ_2DA_ENTRY index 5 9 clab
      DEFINE_ASSOCIATIVE_ARRAY ranger_clabs BEGIN "%clab%" => 0 END
    END 

    PATCH_IF class = class_monk BEGIN
      READ_2DA_ENTRY index 5 9 clab
      DEFINE_ASSOCIATIVE_ARRAY monk_clabs BEGIN "%clab%" => 0 END
    END 

  END
BUT_ONLY

ACTION_PHP_EACH thief_clabs AS key => value BEGIN
  COPY_EXISTING ~%key%.2da~ ~override~
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = "AP_ZSTIVSK0" END
  BUT_ONLY IF_EXISTS
END

ACTION_PHP_EACH ranger_clabs AS key => value BEGIN
  COPY_EXISTING ~%key%.2da~ ~override~
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = "AP_ZSTIVSK0" END
  BUT_ONLY IF_EXISTS
END

ACTION_PHP_EACH monk_clabs AS key => value BEGIN
  COPY_EXISTING ~%key%.2da~ ~override~
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = "AP_ZSTIVSK0" END
  BUT_ONLY IF_EXISTS
END

