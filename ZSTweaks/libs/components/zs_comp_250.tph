ACTION_IF (VARIABLE_IS_SET ~zs_comp_250~) BEGIN
  FAIL ~Script already included: zs_comp_250~
END ELSE BEGIN
  OUTER_SET ~zs_comp_250~ = 1
END

ACTION_IF NOT is_iwdee BEGIN

    COPY ~%MOD_FOLDER%/spells/divine/earthquake~ ~override~

    COPY_EXISTING ~sppr720.spl~ ~override~
        LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@5090) END
        LPF ADD_SPL_TGTEFF STR_VAR effects = ~op=177,res=ZSTWEKG0,p2=5,p1=%class_golem_clay%,ip=0;op=177,res=ZSTWEKG1,p2=5,p1=%class_golem_clay%,ip=0~ END
        LPF ALTER_EFFECT_EX INT_VAR target = 1 STR_VAR match_opcode_ex = "((o_opcode = 269) OR (o_opcode = 272) OR (o_opcode = 174))" END

    ACTION_IF zst_250_quake_pfriendly BEGIN

        COPY_EXISTING ~sppr720.spl~ ~override~
            LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@5090) END
            LPF ALTER_SPELL_HEADER INT_VAR projectile = 159 END
    END

    ACTION_IF zst_250_quake_shake_intensity > 0 BEGIN

        COPY_EXISTING ~sppr720.spl~ ~override~
                      ~spogre01.spl~ ~override~

            PATCH_IF zst_250_quake_shake_intensity = 1 BEGIN
                LPF DELETE_EFFECT INT_VAR match_opcode = 269 END
                LPF DELETE_EFFECT STR_VAR match_resource = ~SHAKE1~ END
            END ELSE PATCH_IF zst_250_quake_shake_intensity = 2 BEGIN
                LPF ALTER_EFFECT INT_VAR match_opcode = 269 parameter1 = (5) END
            END ELSE BEGIN
                PATCH_FAIL "Incorrect setting for Earthquake set: %zst_250_quake_shake_intensity%. Accepted values are 1 (disable) or 2 (highly reduced)"
            END

        BUT_ONLY IF_EXISTS

        COPY_EXISTING ~SHAKE1.EFF~ ~override~
            WRITE_LONG 0x1c 5
        BUT_ONLY IF_EXISTS

    END

    ACTION_IF zst_250_quake_boost BEGIN

        COPY_EXISTING ~SPPR720.SPL~ ~override~
            PATCH_IF zst_250_quake_pfriendly BEGIN
                LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@5091) END
            END ELSE BEGIN
                LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@5092) END
            END

            LPF ALTER_EFFECT INT_VAR match_opcode = 12 match_dicenumber = 3 dicenumber = 5 savebonus="-4" END
            LPF ALTER_EFFECT INT_VAR match_opcode = 12 match_dicenumber = 2 dicenumber = 4 savebonus="-2" END
            LPF ALTER_EFFECT INT_VAR match_opcode = 67 probability1 = 1 END

        ACTION_IF NOT is_bgee BEGIN
            COPY_EXISTING ~SPOGRE01.SPL~ ~override~
                LPF DELETE_EFFECT INT_VAR match_opcode = 269 END
                LPF DELETE_EFFECT STR_VAR match_resource = ~SHAKE1~ END
        END

    END

END
