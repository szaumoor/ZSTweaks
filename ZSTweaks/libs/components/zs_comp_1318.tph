

ACTION_IF is_bg2ee_or_eet BEGIN

    ACTION_IF zst_1318_fnorth_evil OR zst_1318_fnorth_trolls BEGIN

        INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
        COPY_EXISTING ~flamenor.eff~ ~override~
            LPF ALTER_EFF_FILE INT_VAR special=1024 END
        OUTER_SET two_handed_comp_installed = zst_1150_dice_twohanded_swords AND
                                             (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR
                                             (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND zst_group_1150_weapon_dice)) 

    END ELSE BEGIN WARN "No Flame of the North tweaks were selected" END

    ACTION_IF zst_1318_fnorth_evil BEGIN

        COPY_EXISTING ~sw2h12.itm~ ~override~
            SPRINT strref @6170
            LPF ALTER_EFFECT INT_VAR check_globals=0 header_type=1 match_opcode=177 match_parameter2=8 parameter1=alignmen_mask_evil END

            PATCH_IF two_handed_comp_installed BEGIN

                SPRINT str @99999
                SPRINT str_sf @99998
                LPF WPN_DMG RET dmg = result END
                LPF WPN_SPEED RET speed = result END
                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)8~ ~\1%speed%~
                END
                SAY_EVALUATED 0x54 ~%strref%~

            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6170) END
            END

    END

    ACTION_IF zst_1318_fnorth_trolls BEGIN

        COPY ~%MOD_FOLDER%/items/flame_north~ ~override~
        COPY_EXISTING ~sw2h12.itm~ ~override~
            PATCH_IF zst_1318_fnorth_evil BEGIN
                SPRINT strref @6172
            END ELSE BEGIN
                SPRINT strref @6171
            END
            
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=177,tmg=1,p1=129,p2=4,res=ZSTWTRL0~ END

            PATCH_IF two_handed_comp_installed BEGIN

                SPRINT str @99999
                SPRINT str_sf @99998
                LPF WPN_DMG RET dmg = result END
                LPF WPN_SPEED RET speed = result END
                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)8~ ~\1%speed%~
                END
                SAY_EVALUATED 0x54 ~%strref%~
            END ELSE BEGIN

                PATCH_IF zst_1318_fnorth_evil BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6172) END
                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6171) END
                END

            END
    END

END
