ACTION_IF is_bg2ee_or_eet BEGIN

    ACTION_IF zst_1520_keldsword BEGIN
        
        INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
        OUTER_SET two_handed_comp_installed = zst_1150_dice_twohanded_swords AND
                                             (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR
                                             (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND
                                              zst_group_1150_weapon_dice)) 

        COPY_EXISTING "npsw03.itm" ~override~
            LPF ALTER_ITEM INT_VAR ench=3 END
            LPF RM_STAT_REQS END

            PATCH_IF two_handed_comp_installed BEGIN
                SPRINT new_description @6240
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END

                INNER_PATCH_SAVE new_description ~%new_description%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                END
                SAY_EVALUATED 0x54 ~%new_description%~
                
            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6240) END
            END

        // item upgrade patch
        COPY_EXISTING "c2keld02.itm" ~override~
            LPF RM_STAT_REQS END
            LPF ALTER_HEADER INT_VAR dicesize = 10 END

            PATCH_IF two_handed_comp_installed BEGIN
                LPF ALTER_HEADER INT_VAR dicesize = 9 damage = 6 END
                SPRINT new_description @50090
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END

                INNER_PATCH_SAVE new_description ~%new_description%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]6[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                END
                SAY_EVALUATED 0x54 ~%new_description%~

            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@50090) END
            END

            LPF ALTER_EFFECT INT_VAR check_globals = 0 savingthrow = 1 END
            LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 38 probability1 = 29 savingthrow = 0 END
            LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 139 match_parameter1 = 14676 probability1 = 29 savingthrow = 0 END
            LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 142 match_parameter2 = 34 probability1 = 29 savingthrow = 0 END

        BUT_ONLY IF_EXISTS

    END

    ACTION_IF zst_1520_keldarmor BEGIN

        COPY_EXISTING ~npplat.itm~ ~override~
            LPF ALTER_ITEM INT_VAR id_desc = RESOLVE_STR_REF(@6242) END
            LPM remove_haste_immunity

        COPY_EXISTING ~c2keld01.itm~ ~override~
            LPF ALTER_ITEM INT_VAR id_desc = RESOLVE_STR_REF(@50050) END
            LPM remove_haste_immunity
        BUT_ONLY IF_EXISTS

    END

END
