ACTION_IF (VARIABLE_IS_SET ~zs_comp_1321~) BEGIN
  FAIL ~Script already included: zs_comp_1321~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1321~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN
  
  INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
  COPY ~%MOD_FOLDER%/items/bone_club~ ~override~
  COPY_EXISTING ~blun23.itm~ ~override~
    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6361) id_name=RESOLVE_STR_REF(@6360) ench=3 END
    LPF ALTER_HEADER INT_VAR match_damage = 2 match_to_hit = 2 damage = 3 to_hit = 3 speed=1 END
    LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 344 special = 5 END
    LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 177 STR_VAR resource = ~zstwbl03~ END
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 177 STR_VAR resource = ~zstwbl04~ END
    LPF ADD_ON_HIT_EFF INT_VAR p2=4 tmg=1 rd=2 STR_VAR res=~zstwbl04~ effects = ~op=177,p1=%race_spectral_undead%;op=177,p1=%race_specter%;op=177,p1=%race_wraith%;op=177,p1=%race_shadow%;op=177,p2=5,p1=%class_spectral_troll%~ END

    PATCH_IF zst_1450_allow_clubs AND 
            (MOD_IS_INSTALLED "ZSTweaks.tp2" "1450" OR 
            (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~103~ AND zst_group_1450_finesse)) 
    BEGIN
      SET ench = 3
      LPM PROBABILITY_BONUS
      LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode=326 probability1=(bonus+base_ss_club) STR_VAR match_resource=~zstwq2~ resource=~zstwq3~ END
    END

END
