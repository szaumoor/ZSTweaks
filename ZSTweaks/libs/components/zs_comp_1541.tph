ACTION_IF (VARIABLE_IS_SET ~zs_comp_1541~) BEGIN
  FAIL ~Script already included: zs_comp_1541~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1541~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN
    OUTER_SET two_handed_comp_installed = MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1199~
    COPY ~%MOD_FOLDER%/items/vorpal_weapons~ ~override~
    COPY_EXISTING ~BALOR.ITM~ ~override~
        LPF DELETE_EFFECT INT_VAR check_globals = 0 END
        SPRINT effect_str ~op=326,pro1=14,res=ZSTWVRPA~

        PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~3050~ BEGIN
            effect_str = "%effect_str%" + "stype=4,sbonus=-2"
        END

        LPF ADD_ON_HIT_EFF STR_VAR effects = ~%effect_str%~ END

    ACTION_IF NOT MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~3060~ BEGIN

        COPY_EXISTING ~PLANETAR.ITM~ ~override~
            LPF DELETE_EFFECT INT_VAR match_opcode = 13 END
            LPF DELETE_EFFECT INT_VAR match_opcode = 141 match_parameter2 = 39 END
            LPF DELETE_EFFECT INT_VAR match_opcode = 61 END
            LPF DELETE_EFFECT INT_VAR match_opcode = 139 END
            LPF ALTER_EFFECT_EX INT_VAR probability1 = 24 STR_VAR match_probability1_ex = ~(o_probability1 < 100)~ END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=24,res=ZSTWVRPA,stype=4,sbonus=-2~ END

    END

    COPY_EXISTING ~GITH.ITM~ ~override~
        LPF DELETE_EFFECT INT_VAR check_globals = 0 END
        LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=24,res=ZSTWVRPA,stype=4~ END

    COPY_EXISTING ~SW2H15.ITM~ ~override~
        PATCH_IF NOT MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1300~ AND NOT two_handed_comp_installed BEGIN // SILVER SWORD TWEAK

            LPF DELETE_EFFECT INT_VAR check_globals = 0 END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=24,res=ZSTWVRPA,stype=4,sbonus=-2~ END

        END ELSE BEGIN

            LPF DELETE_EFFECT INT_VAR check_globals = 0 match_probability1 = 24 END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=24,res=ZSTWVRPA,stype=4,sbonus=-2~ END

        END

    COPY_EXISTING ~AX1H15.ITM~ ~override~
        LPF DELETE_EFFECT INT_VAR check_globals = 0 END
        LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=9,res=ZSTWVRPA,stype=4,sbonus=-4~ END

    COPY_EXISTING ~HALB11.ITM~ ~override~
        PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1606~ OR two_handed_comp_installed BEGIN

            LPF DELETE_EFFECT INT_VAR match_probability1 = 9 END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=9,res=ZSTWVRPA,stype=4,sbonus=-4,ip=0~ END

        END ELSE BEGIN

            LPF DELETE_EFFECT INT_VAR match_opcode = 13 END
            LPF DELETE_EFFECT INT_VAR match_opcode = 141 match_parameter2 = 39 END
            LPF DELETE_EFFECT INT_VAR match_opcode = 139 END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=9,res=ZSTWVRPA,ip=0~ END

        END

        COPY_EXISTING ~FINSOL02.ITM~ ~override~
            LPF DELETE_EFFECT INT_VAR check_globals = 0 END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=326,pro1=24,res=ZSTWVRPA,stype=4,sbonus=-2~ END

        PRINT "Patching items and spells with immunity to the Vorpal effect..."
        COPY_EXISTING_REGEXP ~^.+\.\(itm\|spl\)$~ ~override~
            READ_ASCII 0 sig (4)
            SET present = 0
            PATCH_MATCH "%sig%" WITH
            "SPL "
            BEGIN
                LPF SPELL_OPCODE_PRESENT INT_VAR opcode = 101 parameter2 = 13 silent=1 RET present = result END
            END

            "ITM "
            BEGIN
                LPF ITM_OPCODE_PRESENT INT_VAR opcode = 101 parameter2 = 13 silent=1 RET present = result END
            END

            DEFAULT
                PATCH_WARN "Found invalid file: %SOURCE_RES%"
            END

            PATCH_IF present BEGIN
                LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=13 opcode=206 parameter2=0 STR_VAR resource="ZSTWVRPA" END
            END
        BUT_ONLY

END



