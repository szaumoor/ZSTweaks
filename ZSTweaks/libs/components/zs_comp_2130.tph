ACTION_IF (VARIABLE_IS_SET ~zs_comp_2130~) BEGIN
  FAIL ~Script already included: zs_comp_2130~
END ELSE BEGIN
  OUTER_SET ~zs_comp_2130~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    ACTION_IF zst_2130_spike_trap BEGIN

        COPY_EXISTING "SPCL910B.SPL" ~override~
            LPF GET_DMG_TYPE STR_VAR type = ~piercing~ RET piercing = result END
            LPF ALTER_EFFECT INT_VAR header_type = 1 check_globals = 0 match_opcode = 12 parameter2 = piercing END

    END

    ACTION_IF zst_2130_exploding_trap BEGIN

        COPY_EXISTING "SPCL911.SPL" ~override~
            LPF ALTER_SPELL INT_VAR ref_desc=RESOLVE_STR_REF(@6300) END

        COPY_EXISTING "SPCL911B.SPL" ~override~
            LPF ALTER_EFFECT INT_VAR header_type=1 check_globals=0 match_opcode=12 dicenumber=15 END
            LPF ALTER_EFFECT INT_VAR header_type=1 check_globals=0 match_opcode=39 duration=6 savingthrow=0 savebonus=0 END

    END

    ACTION_IF zst_2130_assassination BEGIN

        COPY_EXISTING "SPCL916.SPL" ~override~
            LPF ALTER_SPELL INT_VAR ref_desc=RESOLVE_STR_REF(@8080) END
            LPF ALTER_EFFECT INT_VAR match_duration = 6 duration = 7 END
            LPF ALTER_EFFECT INT_VAR resist_dispel = 0 END
            LPF ADD_SPL_EFF_SELF INT_VAR dur=13 rd=0 pwr=0 STR_VAR effects = ~op=301,p1=2;op=362,p1=-1~ END
            LPF ADD_SPL_EFF_SELF INT_VAR pwr=0 rd=0 STR_VAR effects=~op=318,p2=115,p1=%class_thief%~ END
            LPF ADD_SPL_EFF_SELF INT_VAR pwr=0 rd=0 dur=13 STR_VAR effects=~op=1,p1=1;op=278,p1=2;op=73,p1=2;op=301,p1=1~ END
            LPF ADD_SPL_EFF_SELF STR_VAR effects=~op=321,ip=0,rd=0,pwr=0,res=%SOURCE_RES%~ END

    END

    ACTION_IF zst_2130_avoid_death BEGIN

        COPY_EXISTING ~SPCL917.SPL~ ~override~
            LPF ALTER_SPELL INT_VAR ref_desc=RESOLVE_STR_REF(@8180) END
            LPF ADD_SPL_EFF_SELF INT_VAR dur=3 rd=2 STR_VAR effects = ~op=208,p1=1;op=325,p1=100~ END

    END
    
END
