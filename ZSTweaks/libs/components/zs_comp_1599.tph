ACTION_IF (VARIABLE_IS_SET ~zs_comp_1599~) BEGIN
  FAIL ~Script already included: zs_comp_1599~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1599~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    COPY ~%MOD_FOLDER%/items/psion_blade~ ~override~
    COPY_EXISTING ~ZSTWPSIA.SPL~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF(@7691) END
        LPF ALTER_EFFECT INT_VAR match_opcode = 318 parameter2 = zs_has_stoneskins END

    INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
    COPY_EXISTING ~SW2H21.ITM~ ~override~
        LPF ADD_EQUIPPED_EFF INT_VAR p1=race_mind_flayer p2=4 spec=2 STR_VAR effects= ~op=178;op=179~ END
        LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=177,p1=%race_mind_flayer%,p2=4,res=ZSTWPSIB~ END
        LPF ALTER_EFFECT_EX INT_VAR check_headers = 0 duration=0 STR_VAR match_duration_ex="(o_duration > 0)" END

        PATCH_IF zst_1150_dice_twohanded_swords AND
                (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR 
                (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND zst_group_1150_weapon_dice)) 
        BEGIN

            SPRINT strref @7690
            SPRINT str @99999
            LPF WPN_DMG RET dmg = result END
            INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?,[ %TAB%]\+7 vs. Mind Flayers~ ~\11d9+%dmg%, +8 vs. Mind Flayers~
            END
            SAY_EVALUATED 0x54 ~%strref%~

        END ELSE BEGIN
            LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@7690) END
        END
        
END
