ACTION_IF (VARIABLE_IS_SET ~zs_comp_467~) BEGIN
  FAIL ~Script already included: zs_comp_467~
END ELSE BEGIN
  OUTER_SET ~zs_comp_467~ = 1
END

ACTION_IF NOT is_iwdee BEGIN

    WITH_SCOPE BEGIN

        COPY ~%MOD_FOLDER%/spells/divine/sunray~ ~override~
        LAF GET_DMG_TYPE STR_VAR type = "magic" RET magic = result END
        COPY_EXISTING ~SPPR707.SPL~ ~override~
            LPF ALTER_SPELL INT_VAR ref_desc = RESOLVE_STR_REF(@8120) END
            LPF ~CD_TRIM-O-MATIC~ INT_VAR level_cap = 1 END
            LPF ALTER_EFFECT INT_VAR resist_dispel=0 END

            PATCH_IF NOT has_eefixpack BEGIN

                LPF DELETE_EFFECT INT_VAR match_opcode = 177 STR_VAR match_resource = "SP707L14" END
                LPF DELETE_EFFECT INT_VAR match_opcode = 12 END
                LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1=race_vampire parameter2=4 STR_VAR match_resource = "UNDDEATH" END
                LPF CLONE_EFFECT INT_VAR parameter1=race_vampyre STR_VAR match_resource = "UNDDEATH" END

            END ELSE BEGIN
                LPF DELETE_EFFECT INT_VAR match_opcode = 326 STR_VAR match_resource = ~SPPR707A~ END
            END

            LPF ADD_SPL_EFF_TGT INT_VAR pwr=7 STR_VAR effects = ~op=326,p2=2,res=ZSTWSNRA;op=326,p2=1,res=ZSTWSNRC~ END
            LPF ADD_SPL_EFF_TGT INT_VAR rd=0 pwr=7 p2=104 STR_VAR effects=~op=326,p1=%race_vampire%,res=ZSTWSNRB;op=326,p1=%race_vampyre%,res=ZSTWSNRB;op=326,p1=%race_shadow%,res=ZSTWSNRF;op=326,p1=%race_specter%,res=ZSTWSNRF;op=326,p1=%race_wraith%,res=ZSTWSNRF;op=326,p1=%race_spectral_undead%,res=ZSTWSNRF;op=326,p1=%class_spectral_troll%,p2=105,res=ZSTWSNRF;op=326,p1=%race_myconid%,res=ZSTWSNRD~ END

        COPY_EXISTING ~UNDDEATH.EFF~ ~override~
            LPF ALTER_EFF_FILE INT_VAR parameter1=100 parameter2=3 + magic END
            WRITE_LONG 0x38 0
            WRITE_LONG 0x3c 0
            WRITE_LONG 0x24 0

        OUTER_SET weakened = RESOLVE_STR_REF(@8126)
        OUTER_SET dmg7d6  = RESOLVE_STR_REF(@8121)
        OUTER_SET dmg8d6  = RESOLVE_STR_REF(@8122)
        OUTER_SET dmg9d6  = RESOLVE_STR_REF(@8123)
        OUTER_SET dmg10d6 = RESOLVE_STR_REF(@8124)

        COPY_EXISTING ~ZSTWSNRE.SPL~ ~override~
            LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1=weakened END
        BUT_ONLY

        COPY_EXISTING ~ZSTWSNRB.SPL~ ~override~
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=0 parameter1=dmg7d6 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=1 parameter1=dmg7d6 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=2 parameter1=dmg8d6 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=3 parameter1=dmg8d6 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=4 parameter1=dmg9d6 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=5 parameter1=dmg9d6 END
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode = 139 header=6 parameter1=dmg10d6 END
        BUT_ONLY

        COPY_EXISTING ~SPPR609.SPL~ ~override~
            LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicenumber = 5 END
            LPF CLONE_EFFECT INT_VAR match_opcode = 12 parameter2 = magic END
            LPF ALTER_HEADER INT_VAR speed = 4 END
            LPF ALTER_SPELL INT_VAR ref_desc = RESOLVE_STR_REF(@8130) END
    END

END
