ACTION_IF zst_1150_dice_spears BEGIN

    COPY_EXISTING "sper01.itm" ~override~
        SPRINT str @99999
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index

    ACTION_IF has_project_javelin BEGIN

        COPY_EXISTING ~cdjav4e3.eff~ ~override~
                      ~cdjav4e2.eff~ ~override~
                      ~cdjav4e1.eff~ ~override~
                      ~cdjav4e0.eff~ ~override~
                      ~cdjav3e3.eff~ ~override~
                      ~cdjav3e2.eff~ ~override~
                      ~cdjav3e1.eff~ ~override~
                      ~cdjav3e0.eff~ ~override~
            WRITE_LONG 0x3c (THIS + 2) // +2 to dicesize
            WRITE_LONG 0x48 (THIS + 1024) // does not awake sleepers

        COPY_EXISTING ~cdjav0.itm~ ~override~
            READ_STRREF 0x50 desc
            SPRINT str @99999
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4~ ~\11d6~
            END
            SAY_EVALUATED 0x50 ~%desc%~
            READ_LONG 0x50 basic_javelin_description_index

        COPY_EXISTING_REGEXP ~cdjav[0-4][bepq]?.itm~ ~override~
            LPF ALTER_HEADER INT_VAR match_type = 2 match_dicesize = 6 match_dicenumber = 1 dicesize = 8 END
            LPF ALTER_HEADER INT_VAR match_type = 1 match_dicesize = 4 match_dicenumber = 1 dicesize = 6 END

            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "cdjav0" AND NOT "%SOURCE_RES%" STR_EQ "cdjav0q" BEGIN
                LPF ALTER_ITEM INT_VAR general_desc = basic_javelin_description_index END
            END

            PATCH_IF LONG_AT(0x54) != "-1" BEGIN
                READ_STRREF 0x54 desc
                SPRINT str @99999
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4~ ~\11d6~
                END
                SAY_EVALUATED 0x54 ~%desc%~
            END ELSE PATCH_IF NOT "%SOURCE_RES%" STR_EQ "cdjav0" BEGIN
                READ_STRREF 0x50 desc
                SPRINT str @99999
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4~ ~\11d6~
                END
                SAY_EVALUATED 0x50 ~%desc%~
            END

        BUT_ONLY
    END

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_SHORT 0x1c cat
        READ_BYTE 0x31 wpn_prof
        READ_SHORT 0x68 num_abilities

        PATCH_IF cat = 29 AND wpn_prof = 98 AND num_abilities > 0 BEGIN

            PATCH_IF "%SOURCE_RES%" STRING_MATCHES_REGEXP "cdjav[0-4][bepq]?" != 0 BEGIN

                LPF ALTER_HEADER INT_VAR match_type = 1 match_dicesize = 6 match_dicenumber = 1 dicesize = 8 silent = 1 END
                PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sper01" BEGIN

                    LPF ALTER_ITEM INT_VAR general_desc = changed_description_index END
                    READ_LONG 0x54 description_index
                    PATCH_IF description_index > "-1" BEGIN
                        READ_STRREF 0x54 desc
                        SPRINT str @99999
                        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
                        END
                        SAY_EVALUATED 0x54 ~%desc%~
                    END

                END

            END

        END

    BUT_ONLY

END

ACTION_IF zst_1150_dice_warhammers BEGIN

    COPY_EXISTING "hamm01.itm" ~override~
        SPRINT str @99999
        READ_STRREF 0x54 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4~ ~\11d5~
        END
        SAY_EVALUATED 0x54 ~%desc%~
        READ_LONG 0x54 changed_description_index

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_SHORT 0x1c cat
        READ_BYTE 0x31 wpn_prof
        READ_SHORT 0x68 num_abilities
        PATCH_IF cat = 21 AND wpn_prof = 97 AND num_abilities > 0 BEGIN  // warhammers
            LPF ALTER_HEADER INT_VAR match_type = 1 match_dicesize = 4 match_dicenumber = 1 dicesize = 5 silent = 1 END
            LPF ALTER_HEADER INT_VAR match_type = 2 match_dicesize = 4 match_dicenumber = 1 dicesize = 5 silent = 1 END
            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "hamm01" BEGIN
                LPF ALTER_ITEM INT_VAR general_desc = changed_description_index END
                READ_LONG 0x54 description_index
                PATCH_IF description_index > "-1" BEGIN
                    READ_STRREF 0x54 desc
                    SPRINT str @99999
                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4~ ~\11d5~
                    END
                    SAY_EVALUATED 0x54 ~%desc%~
                END
            END
        END
    BUT_ONLY

    //VOIDHAMMER STUFF
    COPY_EXISTING "bdhamm02.itm" ~override~ // voidhammer +3
        LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicesize = 5 END
    BUT_ONLY IF_EXISTS

    COPY_EXISTING ~zstwvd01.spl~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicesize = 5 END
    BUT_ONLY IF_EXISTS
END

ACTION_IF zst_1150_dice_wakininja BEGIN

    COPY_EXISTING "sw1h48.itm" ~override~
        SPRINT str @99999
        SPRINT str_sf @99998
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]8~ ~\12d4~
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)4~ ~\13~ // speed modification
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index_ninjato

    COPY_EXISTING "sw1h46.itm" ~override~
        SPRINT str @99999
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]8~ ~\12d4~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index_waki

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_BYTE 0x31 wpn_prof
        READ_ASCII 0x22 wpn_appearance (2)
        READ_SHORT 0x68 num_abilities
        PATCH_IF num_abilities > 0 BEGIN
            PATCH_IF wpn_prof=95 AND "%wpn_appearance%" STR_EQ "s1" BEGIN // ninjatos
                LPF ALTER_HEADER INT_VAR silent=1 match_type=1 match_dicesize=8 match_dicenumber=1 dicenumber=2 dicesize=4 END
                LPF ALTER_HEADER INT_VAR silent=1 match_type=2 match_dicesize=8 match_dicenumber=1 dicenumber=2 dicesize=4 END
                LPF WPN_SPEED RET s = result END
                PATCH_IF s > 0 BEGIN
                    --s
                    LPF ALTER_HEADER INT_VAR match_type = 1 speed = s END
                END

                PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sw1h48" BEGIN
                    LPF ALTER_ITEM INT_VAR general_desc = changed_description_index_ninjato END
                    READ_LONG 0x54 description_index
                    PATCH_IF description_index > "-1" BEGIN
                        READ_STRREF 0x54 desc
                        SPRINT str_sf @99998
                        SPRINT str @99999
                        LPF WPN_DMG RET dmg = result END
                        LPF WPN_SPEED RET speed = result END
                        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]8~ ~\12d4~
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\1%speed%~
                        END
                        SAY_EVALUATED 0x54 ~%desc%~
                    END
                END

            END ELSE PATCH_IF wpn_prof=95 AND "%wpn_appearance%" STR_EQ "ss" BEGIN // wakis
                LPF ALTER_HEADER INT_VAR silent=1 match_type=1 match_dicesize=8 match_dicenumber=1 dicenumber=2 dicesize=4 END

                PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sw1h48" BEGIN
                    LPF ALTER_ITEM INT_VAR general_desc = changed_description_index_waki END
                    READ_LONG 0x54 description_index
                    PATCH_IF description_index > "-1" BEGIN
                        READ_STRREF 0x54 desc
                        SPRINT str @99999
                        LPF WPN_DMG RET dmg = result END
                        LPF WPN_SPEED RET speed = result END
                        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]8~ ~\12d4~
                        END
                        SAY_EVALUATED 0x54 ~%desc%~
                    END
                END
            END
        END
    BUT_ONLY
END

ACTION_IF zst_1150_dice_wakislashing BEGIN
    COPY_EXISTING "sw1h46.itm" ~override~
        SPRINT str @99999 // damage
        SPRINT str_type @99997
        SPRINT str_new_type @99994
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*.*[ %TAB%]*\)%str_type%~ ~\1%str_new_type%~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index_waki

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_BYTE 0x31 wpn_prof
        READ_ASCII 0x22 wpn_appearance (2)
        READ_SHORT 0x68 num_abilities
        PATCH_IF wpn_prof = 95 AND "%wpn_appearance%" STR_EQ "ss" AND num_abilities > 0 BEGIN // wakizashis
            LPF ALTER_HEADER INT_VAR silent = 1 match_type = 1 damage_type = 7 END

            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sw1h48" BEGIN
                LPF ALTER_ITEM INT_VAR general_desc = changed_description_index_waki END
                READ_LONG 0x54 description_index
                PATCH_IF description_index > "-1" BEGIN
                    READ_STRREF 0x54 desc
                    SPRINT str @99999 // damage
                    SPRINT str_type @99997
                    SPRINT str_new_type @99994
                    SPRINT str_type1 @99995
                    SPRINT str_type2 @99996 // damage type
                    SPRINT str_type3 @99993 // new damage type
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*.*[ %TAB%]*\)%str_type%~ ~\1%str_new_type%~
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_type1%[ %TAB%]*:?[ %TAB%]*\)%str_type2%~ ~\1%str_type3%~
                    END
                    SAY_EVALUATED 0x54 ~%desc%~
                END
            END
        END
    BUT_ONLY
END

ACTION_IF zst_1150_dice_katanas BEGIN

    COPY_EXISTING "sw1h43.itm" ~override~
        SPRINT str @99999
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10~ ~\12d5~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_SHORT 0x1c cat
        READ_BYTE 0x31 wpn_prof
        READ_SHORT 0x68 num_abilities
        PATCH_IF cat = 20 AND wpn_prof = 94 AND num_abilities > 0 BEGIN // katanas
            LPF ALTER_HEADER INT_VAR silent=1 match_type=1 match_dicesize=10 match_dicenumber=1 dicesize=5 dicenumber=2 END
            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sw1h43" BEGIN
                LPF ALTER_ITEM INT_VAR general_desc = changed_description_index END
                READ_LONG 0x54 description_index
                PATCH_IF description_index > "-1" BEGIN
                    READ_STRREF 0x54 desc
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\12d5+%dmg%~
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10,~ ~\11d9+%dmg%,~
                    END
                    SAY_EVALUATED 0x54 ~%desc%~
                END
            END
        END
    BUT_ONLY

END

ACTION_IF zst_1150_dice_bastard_swords BEGIN

    COPY_EXISTING "sw1h01.itm" ~override~
        SPRINT str @99999
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4~ ~\11d8+1~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_SHORT 0x1c cat
        READ_BYTE 0x31 wpn_prof
        READ_SHORT 0x68 num_abilities
        PATCH_IF cat = 20 AND wpn_prof = 89 AND num_abilities > 0  BEGIN // bastard swords
            LPF ALTER_HEADER INT_VAR silent=1 match_type=1 match_dicesize=4 match_dicenumber=2 dicenumber=1 dicesize=8 END
            LPF WPN_DMG RET dmg = result END
            LPF ALTER_HEADER INT_VAR silent=1 match_type=1 damage=dmg+1 END

            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sw1h01" BEGIN
                LPF ALTER_ITEM INT_VAR general_desc = changed_description_index END
                READ_LONG 0x54 description_index
                PATCH_IF description_index > "-1" BEGIN
                    READ_STRREF 0x54 desc
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d8+%dmg%~
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4,~ ~\11d8+%dmg%,~
                    END
                    SAY_EVALUATED 0x54 ~%desc%~
                END
            END
        END
    BUT_ONLY
    
END

ACTION_IF zst_1150_dice_twohanded_swords BEGIN

    COPY_EXISTING "sw2h01.itm" ~override~
        SPRINT str @99999
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10~ ~\11d9+1~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 changed_description_index

    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        READ_SHORT 0x1c cat
        READ_BYTE 0x31 wpn_prof
        READ_SHORT 0x68 num_abilities
        PATCH_IF cat = 20 AND wpn_prof = 93 AND num_abilities > 0 BEGIN // two-handed swords
            LPF ALTER_HEADER INT_VAR silent=1 match_type=1 match_dicesize=12 match_dicenumber=1 dicenumber=2 dicesize=6 END
            LPF WPN_DMG RET dmg = result END
            LPF ALTER_HEADER INT_VAR silent=1 match_type=1 match_dicesize=10 match_dicenumber=1 dicesize=9 damage=dmg+1 END

            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "sw2h01" BEGIN
                LPF ALTER_ITEM INT_VAR general_desc = changed_description_index END
                READ_LONG 0x54 description_index
                PATCH_IF description_index > "-1" BEGIN
                    READ_STRREF 0x54 desc
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END
                    LPF WPN_DICESIZE RET dicesize = result END
                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        PATCH_IF dicesize = 9 BEGIN
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d9+%dmg%~
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10,~ ~\11d9+%dmg%,~
                        END ELSE PATCH_IF dicesize = 6 BEGIN
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]12[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\12d6+%dmg%~
                            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]12,~ ~\12d6,~
                        END
                    END
                    SAY_EVALUATED 0x54 ~%desc%~
                END
            END
        END
    BUT_ONLY

END
