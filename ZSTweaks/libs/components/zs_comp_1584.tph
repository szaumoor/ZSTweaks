ACTION_IF is_bg2ee_or_eet BEGIN

  COPY_EXISTING "ohdsw03.itm" ~override~
    LPF ALTER_EFFECT INT_VAR header_type=1 match_opcode=12 savingthrow = 0 special=1024 parameter1=6 END
    LPF ITM_LAST_HEADER_TYPE INT_VAR type=3 RET header = result END
    LPF ADD_ITEM_HEADER INT_VAR copy_header = header + 1 END
    LPF ALTER_HEADER INT_VAR match_type=3 header=2 primary=6 STR_VAR icon=~spwi523b~ END
    LPF ALTER_EFFECT INT_VAR header_type=3 header=2 match_opcode=146 parameter1=20 parameter2=0 duration=0 timing=0 power=5 STR_VAR resource=~spwi523~ END
    LPF ALTER_HEADER INT_VAR primary=6 range=1000 target=1 STR_VAR match_icon="spwi304b" icon="spwi712b" END
    LPF ALTER_EFFECT INT_VAR header_type=3 header=1 match_opcode=146 parameter1=20 parameter2=0 duration=0 timing=0 power=7 STR_VAR resource=~spwi712~ END

    PATCH_IF zst_1150_dice_bastard_swords AND 
            (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR
            (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND
             zst_group_1150_weapon_dice))
    BEGIN
      SPRINT strref @7510
      SPRINT str @99999
      LPF WPN_DMG RET dmg = result END

      INNER_PATCH_SAVE strref ~%strref%~ BEGIN
        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d8+%dmg%~
      END
      SAY_EVALUATED 0x54 ~%strref%~

    END ELSE BEGIN
      LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@7510) END
    END

END
