ACTION_IF GAME_IS "bg2ee eet" BEGIN

    ACTION_IF _1240_purifier_evil BEGIN

        COPY_EXISTING ~SW1H64.ITM~ ~override~
            SET mask_evil = IDS_OF_SYMBOL(alignmen MASK_EVIL)
            LPF ALTER_EFFECT INT_VAR check_globals=0 header_type=1 match_opcode=177 match_parameter2=8 parameter1=mask_evil END

            PATCH_IF MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "103" OR MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "1400" BEGIN
                SPRINT strref @6090
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END
                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d8+%dmg%~
                END
                SAY_EVALUATED 0x54 ~%strref%~
            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6090) END
            END

        ACTION_IF MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "1241" BEGIN
            OUTER_SPRINT strref @6093
        END ELSE BEGIN
            OUTER_SPRINT strref @6091
        END

        COPY_EXISTING ~SW1H65.ITM~ ~override~
            SET mask_evil = IDS_OF_SYMBOL(alignmen MASK_EVIL)
            LPF ALTER_EFFECT INT_VAR check_globals=0 header_type=1 match_opcode=177 match_parameter2=8 parameter1=mask_evil END

            PATCH_IF MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "103" OR MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "1400" BEGIN
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END
                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d8+%dmg%~
                END
                SAY_EVALUATED 0x54 ~%strref%~
            END ELSE BEGIN
                PATCH_IF MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "1241" BEGIN
                    LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6093) END
                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6091) END
                END
            END

    END

    ACTION_IF _1240_purifier_dispel BEGIN

        ACTION_IF _1240_purifier_evil BEGIN
            OUTER_SPRINT strref @6093
        END ELSE BEGIN
            OUTER_SPRINT strref @6092
        END

        COPY_EXISTING ~SW1H65.ITM~ ~override~
            LPF ALTER_EFFECT INT_VAR header_type=3 match_opcode = 146 parameter1 = 30 STR_VAR match_resource="SPPR303" END

            PATCH_IF MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "103" OR MOD_IS_INSTALLED "%MOD_FOLDER%.tp2" "1400" BEGIN
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END
                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)2[Dd]4[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\11d8+%dmg%~
                END
                SAY_EVALUATED 0x54 ~%strref%~
            END ELSE BEGIN
                PATCH_IF _1240_purifier_evil BEGIN
                    LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6093) END
                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6092) END
                END
            END
    END

    ACTION_IF _1240_purifier_evil OR _1240_purifier_dispel BEGIN
        COPY_EXISTING "SW1H64A.EFF" ~override~
                      "SW1H65A.EFF" ~override~
            LPF ALTER_EFF_FILE INT_VAR special=1024 END
    END
END
