

ACTION_IF is_bg2ee_or_eet BEGIN

    ACTION_IF zst_2270_allow_evasion OR zst_2270_allow_whirlwind BEGIN

        ACTION_CLEAR_ARRAY my_hla_tables
        COPY_EXISTING ~luabbr.2da~ ~override~
            COUNT_2DA_ROWS 2 rows
            FOR ( index = 2; index < rows; ++index ) BEGIN
                READ_2DA_ENTRY index 1 2 abbr
                TO_LOWER abbr
                DEFINE_ASSOCIATIVE_ARRAY my_hla_tables BEGIN "%abbr%" => 1 END
            END

        DEFINE_PATCH_MACRO HLA_EVA_FIX BEGIN END
        DEFINE_PATCH_MACRO HLA_WAR_FIX BEGIN END
 
        ACTION_IF zst_2270_allow_evasion BEGIN

            COPY ~%MOD_FOLDER%/class_kits/evasion~ ~override~
            COPY_EXISTING ~zstweva0.spl~ ~override~
                LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8750) desc = RESOLVE_STR_REF(@8751) END
                
            COPY_EXISTING ~zstweva1.spl~ ~override~
                LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8752) desc = RESOLVE_STR_REF(@8753) END

            COPY_EXISTING ~spcl914.spl~ ~override~
                LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8758) desc = RESOLVE_STR_REF(@8759) END
                LPF ALTER_EFFECT INT_VAR match_opcode = 126 opcode = 176 parameter1 = 6 END

            DEFINE_PATCH_MACRO HLA_EVA_FIX BEGIN
                SET eva0_exists = RESOURCE_CONTAINS "%SOURCE_FILE%" "GA_SPCL913"
                SET eva1_exists = RESOURCE_CONTAINS "%SOURCE_FILE%" "GA_SPCL914"

                PATCH_IF eva0_exists AND eva1_exists BEGIN
                    REPLACE_EVALUATE ~^\(newline\)\|\(GA_SPCL913\)[ %TAB%]~ BEGIN eva0_replace_success = 1 END ~AP_ZSTWEVA0~
                    LPF SWAP_2DA_TABLE_VALUE INT_VAR column=6 start_at_col=1 STR_VAR row_name="AP_ZSTWEVA0" replacement="1" END
                    REPLACE_EVALUATE ~^\(newline\)\|\(GA_SPCL914\)[ %TAB%]~ BEGIN eva1_replace_success = 1 END ~AP_ZSTWEVA1~
                END
            END
        END


        ACTION_IF zst_2270_allow_whirlwind BEGIN
            
            COPY ~%MOD_FOLDER%/class_kits/whirlwind_attack~ ~override~
            COPY_EXISTING ~zstwwhl0.spl~ ~override~
                LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8754) desc = RESOLVE_STR_REF(@8755) END
                
            COPY_EXISTING ~zstwwhl1.spl~ ~override~
                LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8756) desc = RESOLVE_STR_REF(@8757) END

            COPY_EXISTING ~spcl901.spl~ ~override~
                LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8760) desc = RESOLVE_STR_REF(@8761) END

            DEFINE_PATCH_MACRO HLA_WAR_FIX BEGIN                
                SET wh0_exists = RESOURCE_CONTAINS "%SOURCE_FILE%" "GA_SPCL900"
                SET wh1_exists = RESOURCE_CONTAINS "%SOURCE_FILE%" "GA_SPCL901"

                PATCH_IF wh0_exists AND wh1_exists BEGIN
                    REPLACE_EVALUATE ~^\(newline\)\|\(GA_SPCL900\)[ %TAB%]~ BEGIN END ~AP_ZSTWWHL0~
                    LPF SWAP_2DA_TABLE_VALUE INT_VAR column=6 start_at_col=1 STR_VAR row_name="AP_ZSTWWHL0" replacement="1" END
                    REPLACE_EVALUATE ~^\(newline\)\|\(GA_SPCL901\)[ %TAB%]~ BEGIN END ~AP_ZSTWWHL1~
                END

            END

        END 

        ACTION_PHP_EACH my_hla_tables AS abbr => _ BEGIN
            COPY_EXISTING ~lu%abbr%.2da~ ~override~
                LPM HLA_EVA_FIX
                LPM HLA_WAR_FIX
                
        END

    END
     
END