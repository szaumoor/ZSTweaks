ACTION_IF (VARIABLE_IS_SET ~zs_comp_1000~) BEGIN
  FAIL ~Script already included: zs_comp_1000~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1000~ = 1
END

ACTION_IF NOT is_iwdee AND FILE_EXISTS_IN_GAME ~dagg16.itm~ BEGIN

    ACTION_IF zst_1000_sbonus < 0 BEGIN

        COPY_EXISTING "dagg16.itm" ~override~

            LPF ALTER_EFFECT INT_VAR check_globals = 0 header_type = 2 match_savingthrow = 4 savebonus = zst_1000_sbonus END
            
            PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1684~ OR group_component AND zst_group_1684_throwing_dagg_melee BEGIN
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@7352) END
            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6380) END
            END

    END

    ACTION_IF zst_1000_store_additions BEGIN

        ACTION_IF is_bg2ee_or_eet BEGIN

            COPY_EXISTING ~arled.sto~ ~override~
                LPF REPLACE_STORE_ITEM INT_VAR number_in_stock=((30 * zst_1000_stack_mult) / 100) charges1=2 STR_VAR old_item=~dagg16~ new_item=~dagg16~ END

            COPY_EXISTING ~slshop02.sto~ ~override~
                LPF REPLACE_STORE_ITEM INT_VAR number_in_stock=((30 * zst_1000_stack_mult) / 100) charges1=4 STR_VAR old_item=~dagg16~ new_item=~dagg16~ END

            COPY_EXISTING ~shthstor.sto~ ~override~
                LPF REPLACE_STORE_ITEM INT_VAR number_in_stock=((30 * zst_1000_stack_mult) / 100) charges1=3 STR_VAR old_item=~dagg16~ new_item=~dagg16~ END

            COPY_EXISTING ~shop03.sto~ ~override~
                LPF REPLACE_STORE_ITEM INT_VAR number_in_stock=((10 * zst_1000_stack_mult) / 100) charges1=5 STR_VAR old_item=~dagg16~ new_item=~dagg16~ END

            COPY_EXISTING ~bmthief.sto~ ~override~
                LPF REPLACE_STORE_ITEM INT_VAR number_in_stock=((6 * zst_1000_stack_mult) / 100) charges1=6 STR_VAR old_item=~dagg16~ new_item=~dagg16~ END

        END

        ACTION_IF is_bgee_or_eet BEGIN

            COPY_EXISTING ~sto0703.sto~  ~override~
                          ~ulgoth.sto~   ~override~
                          ~highhedg.sto~ ~override~
                LPF ADD_STORE_ITEM_EX INT_VAR stack=((6 * zst_1000_stack_mult) / 100) charge1=5 STR_VAR item_name=~dagg16~ END

            COPY_EXISTING ~stoblack.sto~ ~override~
                          ~stosilen.sto~ ~override~
                LPF ADD_STORE_ITEM_EX INT_VAR stack=((8 * zst_1000_stack_mult) / 100) charge1=6 STR_VAR item_name=~dagg16~ END

        END

    END

END
