ACTION_IF (VARIABLE_IS_SET ~zs_comp_1341~) BEGIN
  FAIL ~Script already included: zs_comp_1341~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1341~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    WITH_SCOPE BEGIN

        OUTER_SET general_tweaks_installed = MOD_IS_INSTALLED "ZSTweaks.tp2" "103"
        OUTER_SET finesse_installed = zst_1450_allow_katanas AND (general_tweaks_installed OR MOD_IS_INSTALLED "ZSTweaks.tp2" "1450")
        OUTER_SET katanas_comp_installed = zst_1150_dice_katanas AND (general_tweaks_installed OR MOD_IS_INSTALLED "ZSTweaks.tp2" "1150")

        LAF GET_DMG_TYPE STR_VAR type = ~magic~ RET magic = result END
        COPY_EXISTING ~SW1H70.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_name=RESOLVE_STR_REF(@6072) ench=4 END
            LPF ALTER_HEADER INT_VAR match_damage = 3 damage = 4 match_to_hit = 3 to_hit = 4 END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=166,p1=5~ END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=178,p1=%general_undead%,p2=3,spec=2~ END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=318,p2=2;op=12,p1=4,p2=%magic%~ END

            PATCH_IF finesse_installed BEGIN // update +4 enchantment
                LPF PROBABILITY_BONUS INT_VAR ench = 4 RET bonus = result END
                LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 326 probability1=(bonus+base_sci_ka) STR_VAR match_resource = ~ZSTWK3~ resource = ~ZSTWK4~  END
            END

            PATCH_IF katanas_comp_installed BEGIN
                SPRINT strref @6070
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END

                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10~ ~\12d5~
                END
                SAY_EVALUATED 0x54 ~%strref%~

            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6070) END
            END

        COPY_EXISTING ~SW1H71.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_name=RESOLVE_STR_REF(@6073) ench=5 END
            LPF ALTER_HEADER INT_VAR match_damage = 4 damage = 5 match_to_hit = 4 to_hit = 5 END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=178,p1=%general_undead%,p2=3,spec=2~ END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=318,p2=2;op=12,p1=6,p2=%magic%~ END

            PATCH_IF finesse_installed BEGIN // update +5 enchantment
                LPF PROBABILITY_BONUS INT_VAR ench = 5 RET bonus = result END
                LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 326 probability1=(bonus+base_sci_ka) STR_VAR match_resource = ~ZSTWK4~ resource = ~ZSTWK5~ END
            END

            PATCH_IF katanas_comp_installed BEGIN
                SPRINT strref @6071
                SPRINT str @99999
                LPF WPN_DMG RET dmg = result END

                INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10~ ~\12d5~
                END
                SAY_EVALUATED 0x54 ~%strref%~

            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@6071) END
            END
    END
    
END
