ACTION_IF (VARIABLE_IS_SET ~zs_comp_2280~) BEGIN
  FAIL ~Script already included: zs_comp_2280~
END ELSE BEGIN
  OUTER_SET ~zs_comp_2280~ = 1
END

COPY ~%MOD_FOLDER%/class_kits/sand_throw~ ~override~
COPY_EXISTING ~zstsand0.spl~ ~override~
    LPF ALTER_SPELL INT_VAR name = RESOLVE_STR_REF(@8880) desc = RESOLVE_STR_REF(@8881) END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 0 parameter1 = RESOLVE_STR_REF(@8882) END

ACTION_CLEAR_ARRAY thief_clabs
ACTION_CLEAR_ARRAY bard_clabs

ACTION_DEFINE_ASSOCIATIVE_ARRAY thief_clabs  BEGIN clabth01, 0 => 1 END
ACTION_DEFINE_ASSOCIATIVE_ARRAY bard_clabs BEGIN clabba01, 0 => 1   END

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR ( index = 2 ; index < rows ; ++index ) BEGIN
    
    READ_2DA_ENTRY index 8 9 class
    PATCH_IF class = class_thief BEGIN
      READ_2DA_ENTRY index 5 9 clab
      DEFINE_ASSOCIATIVE_ARRAY thief_clabs BEGIN "%clab%" => 0 END
    END  

    PATCH_IF class = class_bard BEGIN
      READ_2DA_ENTRY index 5 9 clab
      DEFINE_ASSOCIATIVE_ARRAY bard_clabs BEGIN "%clab%" => 0 END
    END 

  END
BUT_ONLY

ACTION_PHP_EACH thief_clabs AS key => value BEGIN
  COPY_EXISTING ~%key%.2da~ ~override~
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = "AP_ZSTSAND1" END
  BUT_ONLY IF_EXISTS
END

ACTION_PHP_EACH bard_clabs AS key => value BEGIN
  COPY_EXISTING ~%key%.2da~ ~override~
    LPF set_clab_2da_entries INT_VAR f_MinLevel = 1 f_MaxLevel = 1 STR_VAR f_Entry = "AP_ZSTSAND1" END
  BUT_ONLY IF_EXISTS
END


// patch immunities
// fix ranges for spells