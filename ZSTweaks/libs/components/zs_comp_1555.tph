ACTION_IF (VARIABLE_IS_SET ~zs_comp_1555~) BEGIN
  FAIL ~Script already included: zs_comp_1555~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1555~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    COPY_EXISTING ~spin717a.spl~ ~override~ // create weapon, duration (original 40 seconds)
        LPF ALTER_EFFECT INT_VAR match_opcode = 111 duration = 600 END

    COPY_EXISTING ~spin852.spl~ ~override~ // first phase of transformation, holds name and description
                  ~spin822.spl~ ~override~
                  ~spin823.spl~ ~override~
        LPF ALTER_SPELL INT_VAR ref_name = RESOLVE_STR_REF(@8700) ref_desc = RESOLVE_STR_REF(@8701) END

    COPY_EXISTING ~slayerw1.itm~ ~override~ // actual weapon
        LPF ALTER_HEADER INT_VAR dicenumber = 2 dicesize = 6 to_hit = 5 damage = 3 damage_type = 8 END
        LPF ALTER_EFFECT INT_VAR check_headers = 0 resist_dispel = 0 END
        LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode=12 special=1024 timing=0 resist_dispel=0 parameter1=1 END
        LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 33 opcode = 325 parameter1 = 5 parameter2 = 0 END
        PATCH_FOR_EACH op IN 34 35 36 37 BEGIN
            LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode = op END
        END

    COPY_EXISTING ~slayerw1.itm~ ~override/zs_slayw.itm~ // new version at 3 million XP

END
