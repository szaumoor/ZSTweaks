ACTION_IF (VARIABLE_IS_SET ~zs_comp_1583~) BEGIN
  FAIL ~Script already included: zs_comp_1583~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1583~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    OUTER_SET wisdom_drained = RESOLVE_STR_REF(@7165)
    OUTER_SET general_tweaks_installed = MOD_IS_INSTALLED "ZSTweaks.tp2" "103"
    OUTER_SET wpn_dice_comp_installed = (MOD_IS_INSTALLED "ZSTweaks.tp2" "1150" OR (general_tweaks_installed AND zst_group_1150_weapon_dice))

    COPY ~%MOD_FOLDER%/items/kachiko~ ~override~
    COPY_EXISTING "wawak.itm" ~override~
        LPF ALTER_ITEM INT_VAR id_name=RESOLVE_STR_REF(@7160) id_desc=RESOLVE_STR_REF(@7161) END
        LPF ALTER_EFFECT INT_VAR check_globals = 0 match_duration=12 duration = 30 END
        LPF ADD_EQUIPPED_EFF INT_VAR p1=kit_shadowdancer p2=9 STR_VAR effects=~op=177,res=zstwkch0;op=177,res=zstwkch1;op=177,res=zstwkch2~ END
        LPF ADD_ITEM_HEADER INT_VAR type=3 required_id=1 location=3 range=1000 target=4 target_count=1 charges=3 depletion=3 flags=2**11 STR_VAR icon = ~spwi402b~ END
        LPF ADD_ON_HIT_EFF INT_VAR t=1 stype=4 STR_VAR effects = ~op=139,p1=%wisdom_drained%;op=128,dur=6~ END
        LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 pwr=1 rd=1 STR_VAR effects = ~op=124,rd=0;op=20,dur=12;op=362,p1=-1,dur=12;op=174,res=eff_m04,tmg=1;op=215,tmg=1,res=spdhrmny;op=326,p2=109,p1=%kit_shadowdancer%,res=zstwkch3;op=326,p2=105,p1=%class_monk%,res=zstwkch3;op=326,p2=109,p1=%kit_stalker%,res=zstwkch3~ END

        PATCH_IF has_zs_shadowdancer_overhaul BEGIN
            LPF ADD_EQUIPPED_EFF INT_VAR p1=kit_zs_shadowdancer p2=9 STR_VAR effects=~op=177,res=zstwkch0;op=177,res=zstwkch1;op=177,res=zstwkch2~ END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 pwr=1 rd=1 STR_VAR effects = ~op=326,p2=109,p1=%kit_zs_shadowdancer%,res=zstwkch3~ END
        END

        PATCH_IF wpn_dice_comp_installed BEGIN

            PATCH_IF zst_1150_dice_wakininja BEGIN
                SPRINT str @99999
                READ_STRREF 0x54 desc

                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]8~ ~\12d4~
                END
                SAY_EVALUATED 0x54 ~%desc%~

            END ELSE BEGIN
                LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@7161) END
            END

            PATCH_IF zst_1150_dice_wakislashing BEGIN

                SPRINT str @99999 // damage
                SPRINT str_type @99997
                SPRINT str_new_type @99994
                READ_STRREF 0x54 desc

                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*.*[ %TAB%]*\)%str_type%~ ~\1%str_new_type%~
                END
                SAY_EVALUATED 0x54 ~%desc%~

            END
        END

    ACTION_IF zst_1583_kachiko_upgrade BEGIN

        COPY ~%MOD_FOLDER%/items/_upgrades/kachiko~ ~override~
        COPY_EXISTING ~zstwawak.itm~ ~override~
            LPF ALTER_ITEM INT_VAR id_name=RESOLVE_STR_REF(@7163) id_desc=RESOLVE_STR_REF(@7164) END
            LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = wisdom_drained END
            LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_probability1=14 parameter1=RESOLVE_STR_REF(@7167) END
            PATCH_IF general_tweaks_installed OR MOD_IS_INSTALLED "ZSTweaks.tp2" "1450" BEGIN
                SET ench = 5
                LPM PROBABILITY_BONUS
                PATCH_IF NOT zst_1450_allow_bards BEGIN
                    LPF weapon_damage_by_class INT_VAR type=1 probability1=(base_sling_wk_nj+bonus) class=class_thief STR_VAR spell=~zstww5~ END
                END ELSE BEGIN
                    LPF weapon_damage_by_custom INT_VAR type=1 probability1=(base_sling_wk_nj+bonus) custom_p2=zs_thief_or_bard_all STR_VAR spell=~zstww5~ END
                END
            END

            PATCH_IF wpn_dice_comp_installed BEGIN

                PATCH_IF zst_1150_dice_wakininja BEGIN
                    LPF ALTER_HEADER INT_VAR match_type = 1 dicesize = 4 dicenumber = 2 END
                    READ_STRREF 0x54 desc
                    SPRINT str @99999
                    LPF WPN_DMG RET dmg = result END

                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]8~ ~\12d4~
                    END
                    SAY_EVALUATED 0x54 ~%desc%~

                END ELSE BEGIN
                    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@7164) END
                END

                PATCH_IF zst_1150_dice_wakislashing BEGIN

                    LPF ALTER_HEADER INT_VAR damage_type = 7 END
                    SPRINT str @99999 // damage
                    SPRINT str_type @99997
                    SPRINT str_new_type @99994
                    READ_STRREF 0x54 desc

                    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*.*[ %TAB%]*\)%str_type%~ ~\1%str_new_type%~
                    END
                    SAY_EVALUATED 0x54 ~%desc%~

                END
            END

            PATCH_IF has_zs_shadowdancer_overhaul BEGIN
                LPF ADD_EQUIPPED_EFF INT_VAR p1=kit_zs_shadowdancer p2=9 STR_VAR effects=~op=177,res=zstwkch4;op=177,res=zstwkch5;op=177,res=zstwkch6~ END
                LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 pwr=1 rd=1 STR_VAR effects = ~op=326,p2=109,p1=%kit_zs_shadowdancer%,res=zstwkch3~ END
            END

            PATCH_IF spell_shout > 0 BEGIN
                LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_SHOUT" RET spell_res END
                LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = "spwi913" resource = "%spell_res%" END
            END

            PATCH_IF spell_great_shout > 0 BEGIN
                LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_GREAT_SHOUT" RET spell_res END
                LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = "spwi913" resource = "%spell_res%" END
            END

            COMPILE ~%MOD_FOLDER%/dlg/zs_kachiko_upgrade.d~
            EXTEND_BOTTOM ~botsmith.bcs~ ~%MOD_FOLDER%/baf/kachiko/botsmith.baf~

        END

END
