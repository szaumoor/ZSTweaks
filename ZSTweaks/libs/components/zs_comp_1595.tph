ACTION_IF (VARIABLE_IS_SET ~zs_comp_1595~) BEGIN
  FAIL ~Script already included: zs_comp_1595~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1595~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN
    COPY_EXISTING ~SPER09.ITM~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode = 12 probability1 = 49 END
        LPF CLONE_EFFECT INT_VAR match_probability1 = 49 parameter1 = 2 probability2 = 50 probability1 = 79 END
        LPF CLONE_EFFECT INT_VAR match_probability1 = 79 parameter1 = 3 probability2 = 80 probability1 = 100 END
        LPF GET_DMG_TYPE STR_VAR type=~electricity~ RET elec = result END
        LPF ADD_ON_HIT_EFF INT_VAR stype=1 sbonus=2 p1=RESOLVE_STR_REF(@7501) STR_VAR effects = ~op=139,tgt=1~ END
        LPF ADD_ON_HIT_EFF INT_VAR p1=2 p2=elec tmg=3 stype=1 sbonus=2 spec=1024 STR_VAR effects=~op=12,dur=6;op=12,dur=12~ END

        PATCH_IF zst_1150_dice_spears AND (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" OR MOD_IS_INSTALLED "ZSTweaks.tp2" "1150") BEGIN
            SPRINT strref @7500
            SPRINT str @99999
            LPF WPN_DMG RET dmg = result END
            INNER_PATCH_SAVE strref ~%strref%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
            END
            SAY_EVALUATED 0x54 ~%strref%~
        END ELSE BEGIN
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7500) END
        END


END
