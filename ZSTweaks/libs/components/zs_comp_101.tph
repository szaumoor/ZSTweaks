ACTION_IF (VARIABLE_IS_SET ~zs_comp_101~) BEGIN
  FAIL ~Script already included: zs_comp_101~
END ELSE BEGIN
  OUTER_SET ~zs_comp_101~ = 1
END

ACTION_IF zst_101_fblade_undead BEGIN

    COPY ~%MOD_FOLDER%/spells/cleric/flame_blade~ ~override~
    COPY_EXISTING ~SPPR206.SPL~ ~override~
        LPF ALTER_SPELL INT_VAR ref_desc=RESOLVE_STR_REF(@6610) END

    COPY_EXISTING ~FBLADE.ITM~ ~override~
        LPF ALTER_ITEM INT_VAR this_plus=1 flags=2**6 END
        LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=344,p1=4,p2=3,spec=2~ END

        PATCH_IF NOT is_iwdee BEGIN
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=177,rd=2,res=ZSTWFL04,p1=4,p2=3~ END
        END ELSE BEGIN
            LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode=177 STR_VAR match_resource=~FBLADE~ resource=~ZSTWFL04~ END
        END
END

ACTION_IF zst_101_fblade_dur_boost BEGIN

    ACTION_IF NOT is_iwdee BEGIN

        COPY_EXISTING ~SPPR206.SPL~ ~override~
            PATCH_IF MOD_IS_INSTALLED "ZSTweaks.tp2" "101" OR group_component = 1 BEGIN
                LPF ALTER_SPELL INT_VAR ref_desc=RESOLVE_STR_REF(@6630) END
            END ELSE BEGIN
                LPF ALTER_SPELL INT_VAR ref_desc=RESOLVE_STR_REF(@6620) END
            END

            FOR ( level = 3; level <= 19; level += 2 ) BEGIN
                LPF ADD_SPELL_HEADER INT_VAR copy_header = 1 zs_min_level_for_copy = level insert_point = level - 1 END
            END

            LPF SPL_ABILITIES_NUM RET num_abilities = result END
            SET dur = 24
            FOR ( level = 0; level < 20; ++level ) BEGIN
                LPF ALTER_EFFECT INT_VAR header = level duration = dur END
                dur += 6
            END
    END
END

ACTION_IF zst_101_fblade_dur_boost OR zst_101_fblade_undead BEGIN

    COPY_EXISTING ~FBLADE.EFF~ ~override~
        LPF ALTER_EFF_FILE INT_VAR special = 1024 END
    BUT_ONLY IF_EXISTS
    
END
