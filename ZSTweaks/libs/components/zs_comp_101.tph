


ACTION_IF zst_101_fblade_dur_boost OR zst_101_fblade_undead BEGIN

    COPY_EXISTING ~FBLADE.EFF~ ~override~
        LPF ALTER_EFF_FILE INT_VAR special = 1024 END
    BUT_ONLY IF_EXISTS

    ACTION_IF zst_101_fblade_undead AND NOT zst_101_fblade_dur_boost BEGIN
        COPY_EXISTING ~SPPR206.SPL~ ~override~
            LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@6610) END
    END ELSE ACTION_IF NOT zst_101_fblade_undead AND zst_101_fblade_dur_boost BEGIN
        COPY_EXISTING ~SPPR206.SPL~ ~override~
            LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@6620) END
    END ELSE ACTION_IF zst_101_fblade_undead AND zst_101_fblade_dur_boost BEGIN
        COPY_EXISTING ~SPPR206.SPL~ ~override~
            LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@6630) END
    END

    ACTION_IF zst_101_fblade_undead BEGIN

        COPY ~%MOD_FOLDER%/spells/divine/flame_blade~ ~override~
        COPY_EXISTING ~FBLADE.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR this_plus=1 flags=2**6 END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=344,p1=4,p2=3,spec=2~ END

            PATCH_IF NOT is_iwdee BEGIN
                LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=177,rd=2,res=ZSTWFL04,p1=4,p2=3~ END
            END ELSE BEGIN
                LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode=177 STR_VAR match_resource=~FBLADE~ resource=~ZSTWFL04~ END
            END
    END

    ACTION_IF zst_101_fblade_dur_boost BEGIN

        ACTION_IF NOT is_iwdee BEGIN

            COPY_EXISTING ~SPPR206.SPL~ ~override~
                LPF ~CD_TRIM-O-MATIC~ INT_VAR level_cap = 1 END
                LPF ~CD_EXTEND-O-MATIC~ INT_VAR dur_special=1 step_dur=6 level_cap=20 END
                
        END
    END

END
