ACTION_IF (VARIABLE_IS_SET ~zs_comp_1680~) BEGIN
  FAIL ~Script already included: zs_comp_1680~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1680~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

  INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
  COPY_EXISTING "dagg17.ITM" ~override~
    LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6580) END
    LPF ALTER_EFFECT INT_VAR match_opcode = 109 check_globals = 0 probability1 = 24 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 check_globals = 0 probability1 = 24 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 check_globals = 0 probability1 = 24 END
    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=301,p1=1,p2=1~ END
    LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=318,p2=55,res=%SOURCE_RES%;op=12,p1=2,p2=%op12_piercing%,spec=1024;op=142,p2=137,dur=18~ END
    
    SET dur = 6
    FOR ( round = 0; round < 3; ++round ) BEGIN
      LPF ADD_ON_HIT_EFF INT_VAR tmg=3 spec=1024 STR_VAR effects = ~op=12,p1=2,p2=%op12_piercing%,dur=%dur%~ END
      dur += 6
    END

    PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1440~ OR
            (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~103~ AND zst_group_1440_dagger_boost)
    BEGIN
      READ_STRREF 0x54 desc
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*THAC0[ %TAB%]*:?[ %TAB%]*\)\+[0-9]~ ~\1+3~ //thac0
      END
      SAY_EVALUATED 0x54 ~%desc%~
      END

END
