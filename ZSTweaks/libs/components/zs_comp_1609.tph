ACTION_IF is_bg2ee_or_eet BEGIN
  
  INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
  COPY ~%MOD_FOLDER%/items/impaler~ ~override~
  COPY_EXISTING ~sper08.itm~ ~override~
    LPF ALTER_ITEM INT_VAR id_desc = RESOLVE_STR_REF(@8610) END
    LPF ALTER_EFFECT INT_VAR match_opcode = 12 special = (2**10) END
    LPF ADD_EQUIPPED_EFF STR_VAR effects = "op=301,p1=1,p2=1;op=341,p2=1,res=zstwimpa" END

    PATCH_IF zst_1150_dice_spears AND 
            (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR 
            (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND zst_group_1150_weapon_dice))
    BEGIN
      READ_STRREF 0x54 desc
      SPRINT str @99999
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
      END
      SAY_EVALUATED 0x54 ~%desc%~
    END

END
