ACTION_IF (VARIABLE_IS_SET ~zs_comp_1682~) BEGIN
  FAIL ~Script already included: zs_comp_1682~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1682~ = 1
END

ACTION_IF NOT is_iwdee BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~bddagg05.itm~ BEGIN
        
        INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
        COPY_EXISTING ~bddagg05.itm~ ~override~
            LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6941) id_name=RESOLVE_STR_REF(@6940) ench=3 END

            SET general_tweaks_installed = MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~103~
            PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1440~ OR
                    (general_tweaks_installed AND zst_group_1440_dagger_boost)
            BEGIN
                LPF ALTER_HEADER INT_VAR match_type = 1 damage = 3 to_hit = 4  END
                LPF ALTER_EFFECT STR_VAR match_resource = ~ZSTWDPB2~ resource = ~ZSTWDPB3~ END
                LPF ALTER_EFFECT STR_VAR match_resource = ~ZSTWDPT2~ resource = ~ZSTWDPT3~ END

                READ_STRREF 0x54 desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*THAC0[ %TAB%]*:?[ %TAB%]*\)\+[0-9]~ ~\1+4~ //thac0
                END
                SAY_EVALUATED 0x54 ~%desc%~

            END ELSE BEGIN
                LPF ALTER_HEADER INT_VAR match_type = 1 damage = 3 to_hit = 3  END
            END

            LPF ALTER_EFFECT INT_VAR match_opcode = 12 parameter1 = 0 dicenumber = 1 dicesize = 3 special=1024 END
            LPF ALTER_EFFECT INT_VAR match_opcode = 146 probability1 = 19 END

            PATCH_IF zst_1450_allow_daggers AND 
                    (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1450~ OR
                    (general_tweaks_installed AND zst_group_1450_finesse))
            BEGIN
                SET ench = 3
                LPM PROBABILITY_BONUS
                LPF ALTER_EFFECT INT_VAR probability1 =(bonus+base_dagg_dart) STR_VAR match_resource = ~ZSTWD2~ resource = ~ZSTWD3~  END
            END

        COPY_EXISTING ~bddagg05.spl~ ~override~
            LPF ALTER_EFFECT INT_VAR match_opcode = 12 parameter1 = 0 dicenumber = 1 dicesize = 3 special=1024 END
            LPF ADD_SPL_TGTEFF INT_VAR rd=0 pwr=0 STR_VAR effects = ~op=0,dur=18,p1=-2,ip=3~ END

    END ELSE BEGIN
        ACTION_IF NOT group_component BEGIN WARN "bddagg05.itm not found in game files, skipping..." END
    END

END
