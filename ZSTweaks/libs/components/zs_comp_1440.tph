COPY ~%MOD_FOLDER%/items/dagger_boost~ ~override~
COPY_EXISTING_REGEXP "ZSTWDP08.spl" ~override~ // spells to insert projectiles to (critical dagger effect)
    LPF ALTER_HEADER INT_VAR match_type = 1 projectile =1 END
BUT_ONLY
COPY_EXISTING_REGEXP "ZSTWDP1[02468]\.spl" ~override~ // spells to insert projectiles to (throwing daggers and darts)
    LPF ALTER_HEADER INT_VAR match_type = 1 projectile = 1 END
BUT_ONLY
COPY_EXISTING "ZSTWDP20.spl" ~override~ // spells to insert projectiles to (critical dagger effect)
    LPF ALTER_HEADER INT_VAR match_type = 1 projectile = 1 END
BUT_ONLY

COPY_EXISTING "DAGG01.ITM" ~override~
    SPRINT str_sf @99998
    READ_STRREF 0x50 desc
    LPF ITM_REF_UDESC RET basic_dagg_udesc = result END
    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)2~ ~\10~ // speed modification
    END
    SAY_EVALUATED 0x50 ~%desc%~
    LPF ITM_REF_UDESC RET changed_description_index = result END

COPY_EXISTING_REGEXP ".*\.itm" ~override~
    LPF ITM_CAT RET wpn_cat = result END
    LPF ITM_PROF RET wpn_prof = result END
    LPF ITM_ENCH RET ench = result END
	PATCH_IF wpn_cat = 16 AND wpn_prof = 96 BEGIN // daggers
        LPF ITM_REF_UDESC RET id_udesc = result END
        PATCH_IF (NOT "%SOURCE_RES%" STR_EQ "DAGG01") AND id_udesc = basic_dagg_udesc BEGIN
            LPF ALTER_ITEM INT_VAR unid_ref_desc = changed_description_index END
        END
        PATCH_IF NOT "%SOURCE_RES%" STR_EQ "dagg04" // not grave binder (fake dagger) exceptions
             AND NOT "%SOURCE_RES%" STR_EQ "a7-dg05" // Entropic Blade (wares of the planes)
             AND NOT "%SOURCE_RES%" STR_EQ "a7-dg06" // Enlightenment (wares of the planes)
        BEGIN
            LPF WPN_THAC0 RET thac0 = result END
            LPF WPN_SPEED RET speed = result END
            SET actual_speed = (speed - 2)
            PATCH_IF actual_speed < 0 BEGIN
                actual_speed = 0
            END
            LPF ALTER_HEADER INT_VAR silent = 1 match_type = 1 speed = actual_speed to_hit = thac0+1 END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=301,p1=1,p2=1~ END
            PATCH_MATCH ench
               WITH 0 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP08~ END
                END 1 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP10~ END
                END 2 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP12~ END
                END 3 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP14~ END
                END 4 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP16~ END
                END 5 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP18~ END
                END 6 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP20~ END
                END DEFAULT
                    LPF ITM_STR_NAME RET name = result END
                    PATCH_WARN "[Dagger boost] Interesting, we found a weapon with more than +6 in enchantment: %SOURCE_RES%, %name%"
            END

            LPF ITM_REF_DESC RET desc_index = result END
            LPF ITM_REF_UDESC RET udesc_index = result END

            PATCH_IF desc_index > "-1" BEGIN
                READ_STRREF 0x54 desc
                SPRINT str_sf @99998
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\1%actual_speed%~ // speed
                END
                SAY_EVALUATED 0x54 ~%desc%~
            END ELSE PATCH_IF desc_index = "-1" AND udesc_index != basic_dagg_udesc BEGIN
                READ_STRREF 0x50 desc
                SPRINT str_sf @99998
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\1%actual_speed%~ // speed
                END
                SAY_EVALUATED 0x50 ~%desc%~
            END
        END
    END
BUT_ONLY
