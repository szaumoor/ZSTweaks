

OUTER_SET new_general_desc = RESOLVE_STR_REF(@8365)
COPY_EXISTING "DAGG01.ITM" ~override~
    READ_LONG 0x50 dagg_original_desc_index
    LPF ALTER_ITEM INT_VAR general_desc = new_general_desc END

COPY ~%MOD_FOLDER%/items/dagger_boost~ ~override~
COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
    READ_SHORT 0x1c cat
    READ_BYTE 0x31 wpn_prof
    READ_SHORT 0x68 num_abilities
    READ_ASCII 0x22 appearance (2)
    READ_LONG 0x60 ench

	PATCH_IF cat = 16 AND wpn_prof = 96 AND ench <= 6 AND num_abilities > 0 AND NOT ("%appearance%" STR_EQ "  ")
             AND NOT ("%SOURCE_RES%" STR_EQ "ZSIPKUK0") // KUKRI exceptions
             AND NOT ("%SOURCE_RES%" STR_EQ "ZSIPKUK1") // KUKRI exceptions
             AND NOT ("%SOURCE_RES%" STR_EQ "ZSIPKUK2") // KUKRI exceptions
             AND NOT ("%SOURCE_RES%" STR_EQ "ZSIPKUK3") // KUKRI exceptions
             AND NOT ("%SOURCE_RES%" STR_EQ "ZSIPKRE0") // KUKRI ECLIPSE exception
             AND NOT ("%SOURCE_RES%" STR_EQ "ZSIPKRE2") // KUKRI ECLIPSE exception
    BEGIN

        READ_LONG 0x50 id_udesc
        PATCH_IF (NOT "%SOURCE_RES%" STR_EQ "DAGG01") AND id_udesc = dagg_original_desc_index BEGIN
            LPF ALTER_ITEM INT_VAR general_desc = new_general_desc END
        END

        PATCH_IF NOT "%SOURCE_RES%" STR_EQ "dagg04" // not grave binder (fake dagger) exceptions
             AND NOT "%SOURCE_RES%" STR_EQ "a7-dg05" // Entropic Blade (wares of the planes)
             AND NOT "%SOURCE_RES%" STR_EQ "a7-dg06" // Enlightenment (wares of the planes)
             AND NOT "%SOURCE_RES%" STR_EQ "AGMOOSE" // moose dagger (ajoc mod)
        BEGIN

            LPF ALTER_HEADER_EX STR_VAR match_type_ex=~(o_type = 1 OR o_type = 2)~ to_hit_ex = ~(THIS + 1)~ speed_ex = ~((speed > 1) ? (THIS - 2) : 0)~ END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=301,p1=1,p2=1;op=341,p2=1,res=ZSTWDPB%ench%,spec=1;op=341,p2=1,res=ZSTWDPT%ench%,spec=2~ END

            READ_LONG 0x54 desc_index
            READ_LONG 0x50 udesc_index
            PATCH_IF desc_index > "-1" BEGIN
                LPF WPN_SPEED RET actual_speed = result END
                LPF WPN_THAC0 RET thac0_bonus = result END

                READ_STRREF 0x54 desc
                SPRINT str_sf @99998
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\1%actual_speed%~ // speed
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*THAC0[ %TAB%]*:?[ %TAB%]*\)\+[0-9]~ ~\1+%thac0_bonus%~ //thac0
                END
                SAY_EVALUATED 0x54 ~%desc%~

            END ELSE PATCH_IF desc_index = "-1" AND udesc_index != dagg_original_desc_index BEGIN

                READ_STRREF 0x50 desc
                SPRINT str_sf @99998
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\1%actual_speed%~ // speed
                END
                SAY_EVALUATED 0x50 ~%desc%~

            END

        END

    END

BUT_ONLY

ACTION_IF has_zs_item_pack_kukris BEGIN

    COPY_EXISTING ~ZSIPKUK0.ITM~ ~override~
        READ_STRREF 0x50 desc
        SPRINT str @99999
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4~ ~\11d4+1~
        END
        SAY_EVALUATED 0x50 ~%desc%~
        READ_LONG 0x50 kukri_unid_desc

    COPY_EXISTING_REGEXP ~^ZSIPK[UR][KE][0-3]\.ITM~ ~override~
        LPF ALTER_HEADER_EX STR_VAR damage_ex = ~(THIS + 1)~ speed_ex = ~((o_speed > 0) ? (THIS - 1) : 0)~ END
        READ_LONG 0x60 ench
        SET damage_bonus = ench + 1
        SET speed = (   (2 - ench) < 0 ? 0 : (2 - ench)    )
        READ_STRREF 0x54 desc
        SPRINT str @99999
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\1%speed%~ // speed
            REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]4\+?[1-9]?~ ~\11d4+%damage_bonus%~
        END
        SAY_EVALUATED 0x54 ~%desc%~
        LPF ALTER_ITEM INT_VAR general_desc = kukri_unid_desc END
        LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDPB%ench%,spec=1;op=341,p2=1,res=ZSTWDPT%ench%,spec=2~ END

    BUT_ONLY IF_EXISTS

END

ACTION_IF is_bg2ee_or_eet BEGIN

    COPY_EXISTING ~DAGG18.ITM~ ~override~
        LPF ALTER_ITEM INT_VAR general_desc = RESOLVE_STR_REF(@8366) END

    COPY_EXISTING ~DAGG19.ITM~ ~override~
        LPF ALTER_ITEM INT_VAR general_desc = RESOLVE_STR_REF(@8367) END

END


