ACTION_IF (VARIABLE_IS_SET ~zs_comp_1534~) BEGIN
  FAIL ~Script already included: zs_comp_1534~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1534~ = 1
END

ACTION_IF zst_1534_potions_usable_everyone BEGIN
    COPY_EXISTING_REGEXP "^.+\.itm$" ~override~
        LPF ITM_CAT RET cat = result END
        PATCH_IF cat = 9 BEGIN
            LPF ALTER_ITEM INT_VAR unusable0=0 END
        END
    BUT_ONLY
END

ACTION_IF zst_1534_potions_cannot_self_stack BEGIN
    COPY_EXISTING_REGEXP ~^.+\.itm$~ ~override~
        LPF ITM_CAT RET category_itm=result END
        PATCH_IF category_itm = 9 OR category_itm = 13 BEGIN // if it's a potion or food item
            PATCH_IF NOT "%SOURCE_RES%" STR_EQ "GBERRY" BEGIN
                LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=321,t=3,tgt=1,res=%SOURCE_RES%,ip=0~ END
            END
        END
    BUT_ONLY

    ACTION_IF NOT MOD_IS_INSTALLED "ZSTweaks.tp2" "240" AND NOT MOD_IS_INSTALLED "ZSTweaks.tp2" "99" BEGIN
        COPY_EXISTING ~GBERRY.ITM~ ~override~
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=321,t=3,tgt=1,res=%SOURCE_RES%,ip=0~ END
        BUT_ONLY IF_EXISTS
    END
END

ACTION_IF zst_1534_potion_overhaul BEGIN
    ACTION_IF NOT is_iwdee BEGIN // iwdee deliberately unsupported for now
        //icedust potion
        ACTION_IF FILE_EXISTS_IN_GAME ~WAND16.ITM~ BEGIN
            COPY_EXISTING ~WAND16.ITM~ ~override~
                LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7461) END
            COPY_EXISTING ~SPIN713.SPL~ ~override~
                LPF ALTER_HEADER INT_VAR match_type = 2 projectile = 160 END
                LPF ALTER_EFFECT INT_VAR match_opcode = 30 duration = 18 parameter1=75 parameter2=0 END
                LPF ALTER_EFFECT INT_VAR match_opcode = 141 parameter2 = 29 END
                LPF DELETE_EFFECT INT_VAR match_opcode = 61 END
                LPF RGB INT_VAR red = 50 green = 181 blue = 199 RET color = result END
                SET fade_speed = ( 25 << 16 )
                LPF ADD_SPL_EFF_TGT INT_VAR pwr=4 STR_VAR effects=~op=142,p2=16,t=2,dur=18;op=61,p1=%color%,p2=%fade_speed%,tmg=1,rd=1~ END
        END

        // charname's tankard
        COPY_EXISTING "ohntank.ITM" ~override~
            LPF ALTER_ITEM INT_VAR unid_ref_desc=RESOLVE_STR_REF(@6450) END
            LPF ALTER_EFFECT INT_VAR check_globals=0 header_type=3 match_opcode=17 dicesize=0 dicenumber=0 parameter1=27 END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 p1=1 tgt=1 dur=60 tmg=0 STR_VAR effects=~op=10;op=44~ END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 tmg=0 STR_VAR effects=~op=321,res=%SOURCE_RES%,ip=0~ END
        BUT_ONLY IF_EXISTS

        // potion of healing (basic)
        COPY_EXISTING ~POTN08.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7467) END
            LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=12 END

        COPY_EXISTING ~POTN25.ITM~ ~override~ // oddly murky potion consistency description change
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7468) END

        // potion of heroism
        COPY_EXISTING ~POTN09.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7462) unusable0=0 END
            LPF ALTER_EFFECT INT_VAR match_opcode = 54 parameter1 = 2 parameter2= 0 END
            LPF ADD_ON_HIT_EFF INT_VAR dur=600 rd=1 pwr=4 t=3 tgt=1 STR_VAR effects=~op=23,spec=1;op=73,p1=1~ END

        // potion of invunerability
        COPY_EXISTING ~POTN11.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7463) unusable0=0 END
            LPF ADD_ON_HIT_EFF INT_VAR dur=300 rd=1 pwr=4 t=3 tgt=1 STR_VAR effects=~op=166,p1=10~ END

        // elixir of health
        COPY_EXISTING ~POTN17.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7464) END
            LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=20 END
            LPF ALTER_EFFECT INT_VAR silent=1 match_opcode = 94 parameter2=0 opcode = 164 END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 dur=10 rd=0 pwr=4 STR_VAR effects=~op=169,p2=137;op=169,p2=6;op=169,p2=7;op=101,p2=78;op=101,p2=25~ END

        // potion of perception
        COPY_EXISTING ~POTN39.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7465) END
            LPF ALTER_EFFECT INT_VAR match_opcode=91 parameter1=40 END
            LPF CLONE_EFFECT INT_VAR match_opcode=91 opcode=276 parameter1=40 END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 p1=2 dur=1800 rd=1 pwr=4 STR_VAR effects=~op=0,p1=1;op=325;op=278~ END

        //potion of power
        COPY_EXISTING ~POTN41.ITM~ ~override~
            LPF ALTER_ITEM INT_VAR id_ref_desc=RESOLVE_STR_REF(@7466) END
            LPF ALTER_EFFECT INT_VAR match_opcode = 54 parameter1 = 4 parameter2= 0 END
            LPF CLONE_EFFECT INT_VAR match_opcode=92 opcode = 277 END
            LPF CLONE_EFFECT INT_VAR match_opcode=92 opcode = 276 END
            LPF CLONE_EFFECT INT_VAR match_opcode=92 opcode = 59 END
            LPF CLONE_EFFECT INT_VAR match_opcode=92 opcode = 332 parameter1=10 parameter2=0 END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 dur=240 pwr=4 STR_VAR effects=~op=23,spec=1;op=189,p1=1~ END

        ACTION_IF NOT zst_1534_potions_usable_everyone BEGIN
            COPY_EXISTING_REGEXP "^POTN0[3-7]\.itm$" ~override~
                LPF ALTER_ITEM INT_VAR unusable0=0 END
            BUT_ONLY

            COPY_EXISTING "POTN12.ITM" ~override~
                LPF ALTER_ITEM INT_VAR unusable0=0 END
        END

        COPY_EXISTING ~POTN13.ITM~ ~override~ // vanilla explosive / fire breath potions
                      ~POTN26.ITM~ ~override~
                      ~POTN27.ITM~ ~override~
            LPF ALTER_EFFECT INT_VAR check_globals = 0 resist_dispel = 0 END

    END ELSE BEGIN

        ACTION_IF group_component = 0 BEGIN
            PRINT "Potion Overhaul is currently not supported for IWDEE due to lack of testing"
        END

    END
END
