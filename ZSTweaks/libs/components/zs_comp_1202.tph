ACTION_IF (VARIABLE_IS_SET ~zs_comp_1202~) BEGIN
  FAIL ~Script already included: zs_comp_1202~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1202~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

  INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
  COPY ~%MOD_FOLDER%/items/warblade~ ~override~
  COPY_EXISTING ~SW2H09.ITM~ ~override~
    LPF ALTER_ITEM INT_VAR id_name=RESOLVE_STR_REF(@6860) price=19500 END
    LPF ALTER_HEADER INT_VAR match_damage = 4 damage = 2 END
    LPF ADD_ON_HIT_EFF STR_VAR effects = ~op=12,tmg=0,dsize=4,dnum=1,p2=%op12_cold%,spec=1024~ END
    LPF ADD_EQUIPPED_EFF INT_VAR p1=kit_barbarian p2=9 STR_VAR effects=~op=177,res=ZSTWWARA;op=177,res=ZSTWWARB;op=177,res=ZSTWWARC;op=177,res=ZSTWWARD;op=177,res=ZSTWWARE~ END

    PATCH_IF zst_1150_dice_twohanded_swords AND
            (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR
            (MOD_IS_INSTALLED "ZSTweaks.tp2" "103" AND zst_group_1150_weapon_dice)) 
    BEGIN
      SPRINT new_description @6861
      SPRINT str @99999
      LPF WPN_DMG RET dmg = result END
      INNER_PATCH_SAVE new_description ~%new_description%~ BEGIN
          REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]12[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?~ ~\12d6+%dmg%~
      END
      SAY_EVALUATED 0x54 ~%new_description%~
    END ELSE BEGIN
      LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6861) END
    END
    
END
