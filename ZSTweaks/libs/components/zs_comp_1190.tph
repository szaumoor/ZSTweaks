ACTION_IF (VARIABLE_IS_SET ~zs_comp_1190~) BEGIN
  FAIL ~Script already included: zs_comp_1190~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1190~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
    
    OUTER_SET general_tweaks_installed = MOD_IS_INSTALLED "ZSTweaks.tp2" "103"
    OUTER_SET finesse_component_installed = (MOD_IS_INSTALLED "ZSTweaks.tp2" "1450" OR (general_tweaks_installed AND zst_group_1450_finesse))
    OUTER_SET weapon_dice_installed = (MOD_IS_INSTALLED "ZSTweaks.tp2" "1150" OR (general_tweaks_installed AND zst_group_1150_weapon_dice))

    COPY ~%MOD_FOLDER%/items/rod_might~ ~override~
    COPY_EXISTING ~RODS02.ITM~ ~override~
                ~rods02a.ITM~ ~override~
        LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6040) id_name = RESOLVE_STR_REF(@6048) unusable0=0 END

    COPY_EXISTING ~RODMACE.ITM~ ~override~
        SET name = RESOLVE_STR_REF(@6041)
        SET desc = RESOLVE_STR_REF(@6044)
        LPF ALTER_ITEM INT_VAR id_desc=desc general_desc=desc general_name=name id_name=name ench=3 STR_VAR icon=~ZSTWRRM0~ desc_img=~ZSTWRRM1~ END
        LPF ALTER_HEADER INT_VAR match_type = 1 speed = 4 damage = 4 STR_VAR icon = ~ZSTWRRM1~ END
        LPF ALTER_EFFECT INT_VAR header_type=1 check_globals=0 match_opcode=24 opcode=40 savebonus=2 duration=12 END
        LPF ALTER_EFFECT INT_VAR match_opcode = 7 parameter1=44 END

    COPY_EXISTING ~RODSWORD.ITM~ ~override~
        SET name = RESOLVE_STR_REF(@6042)
        SET desc = RESOLVE_STR_REF(@6045)
        LPF ALTER_ITEM INT_VAR id_desc=desc general_desc=desc general_name=name id_name=name ench=3 STR_VAR appearance="FS" icon=~ZSTWRRM2~ desc_img=~ZSTWRRM3~ END
        LPF ALTER_HEADER INT_VAR match_type = 1 speed = 2 damage = 3 STR_VAR icon = ~ZSTWRRM2~ END
        LPF ALTER_EFFECT INT_VAR header_type=1 check_globals=0 match_savingthrow=1 savebonus=2 duration=6 resist_dispel=3 END
        LPF ALTER_EFFECT INT_VAR match_opcode = 139 check_globals = 0 duration = 0 parameter1 = 1280 probability1 = 49 END
        LPF ALTER_EFFECT INT_VAR match_opcode = 7 parameter1=44 END

        PATCH_IF NOT has_eefixpack BEGIN
            LPF DELETE_EFFECT INT_VAR check_globals = 0 header_type = 1 match_opcode = 109 multi_match = 1 END
        END

        LPF ALTER_EFFECT INT_VAR match_opcode = 109 opcode = 45 probability1 = 49 END
        LPF CLONE_EFFECT INT_VAR match_opcode = 45 opcode = 39 parameter2 = 1 probability1 = 100 probability2 = 50 END
        LPF ADD_ON_HIT_EFF STR_VAR effects =~op=12,dsize=4,dnum=1,p2=%op12_fire%,spec=1024~ END

        PATCH_IF zst_1450_allow_lswords AND finesse_component_installed BEGIN
            SET ench = 3
            LPM PROBABILITY_BONUS
            LPF ALTER_EFFECT INT_VAR match_opcode=326 probability1=(bonus+base_ls_staff) STR_VAR match_resource="ZSTWL1" resource="ZSTWL3" END
        END

    COPY_EXISTING ~RODSPEAR.ITM~ ~override~
        SET name = RESOLVE_STR_REF(@6043)
        SET desc = RESOLVE_STR_REF(@6046)
        LPF ALTER_ITEM INT_VAR general_name=name id_name=name general_desc=desc id_desc=desc STR_VAR icon=~ZSTWRRM4~ desc_img=~ZSTWRRM5~ END
        LPF ALTER_HEADER INT_VAR match_type = 1 overhand = 25 thrust = 75 STR_VAR icon = ~ZSTWRRM4~ END
        LPF ALTER_EFFECT INT_VAR check_globals=0 match_opcode=12 savebonus=0 savingthrow=0 parameter1=2 special=1024 probability1=9 END
        LPF ALTER_EFFECT INT_VAR match_opcode = 7 parameter1=44 END
        LPF ADD_ON_HIT_EFF INT_VAR p2=104 STR_VAR effects = ~op=318,p2=55;op=318,p2=9;op=318,p1=%race_shambling_mound%;op=318,p1=%race_slime%~ END
        LPF ADD_ON_HIT_EFF INT_VAR p1=2 p2=op12_piercing tmg=3 stype=2 spec=1024 STR_VAR effects = ~op=12,tmg=0,p1=1,stype=0;op=12,p1=1,dur=1;op=12,dur=2;op=12,dur=3;op=12,dur=4;op=12,dur=5;op=12,p1=1,dur=6~ END

        PATCH_IF zst_1150_dice_spears AND weapon_dice_installed BEGIN
            SPRINT desc @6046
            SPRINT str @99999
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d8~
            END
            SAY_EVALUATED 0x54 ~%desc%~
            SAY_EVALUATED 0x50 ~%desc%~
        END ELSE BEGIN
            LPF ALTER_ITEM INT_VAR id_desc=desc END
        END

END
