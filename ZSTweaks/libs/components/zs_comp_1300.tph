ACTION_IF is_bg2ee_or_eet BEGIN

    COPY ~%MOD_FOLDER%/items/silver_sword~ ~override~
    COPY_EXISTING ~zstwswsa.spl~ ~override~
        LPF ALTER_EFFECT INT_VAR match_opcode=139 match_savebonus=0 match_savingthrow=0 parameter1=RESOLVE_STR_REF(@7682) END

    INCLUDE "%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt"
    COPY_EXISTING ~sw2h15.itm~ ~override~
        LPF ALTER_ITEM INT_VAR id_name=RESOLVE_STR_REF(@7680) price=10000 END

        SET general_comp_installed = MOD_IS_INSTALLED "ZSTweaks.tp2" "103"
        SET vorpal_comp_installed = MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1541~ OR
                                   (general_comp_installed AND zst_group_1541_vorpal_tweak)
                                   
        PATCH_IF NOT vorpal_comp_installed  BEGIN
            LPF ALTER_EFFECT_EX INT_VAR probability1=24 STR_VAR match_probability1_ex = "(o_probability1 < 100)" END
        END

        LPF ALTER_HEADER INT_VAR type = 1 speed = 6 END
        LPF ADD_EQUIPPED_EFF INT_VAR p1=race_mind_flayer p2=4 spec=2 STR_VAR effects= ~op=178;op=179;op=344,spec=5~ END
        LPF ADD_EQUIPPED_EFF STR_VAR effects=~op=341,p2=1,spec=1,res=zstwswsa;op=206,res=spin975;op=206,res=spin974;op=206,res=spin910;op=206,res=spin959;op=206,res=spin834;op=206,res=spin775~ END

        PATCH_IF zst_1150_dice_twohanded_swords AND
                (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1150~ OR
                (general_comp_installed AND zst_group_1150_weapon_dice)) 
        BEGIN

            SPRINT new_description @7681
            SPRINT str @99999
            SPRINT str_sf @99998
            LPF WPN_DMG RET dmg = result END
            LPF WPN_SPEED RET speed = result END
            INNER_PATCH_SAVE new_description ~%new_description%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]10[ %TAB%]?\+[ %TAB%]?[0-9][0-9]?,[ %TAB%]\+5 vs. Mind Flayers~ ~\11d9+%dmg%, +6 vs. Mind Flayers~
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)8~ ~\1%speed%~
            END
            SAY_EVALUATED 0x54 ~%new_description%~

        END ELSE BEGIN
            LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@7681) END
        END

END
