ACTION_IF is_bg2ee_or_eet BEGIN

    WITH_SCOPE BEGIN
        OUTER_SET general_tweaks_installed = MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~103~
        ACTION_IF general_tweaks_installed BEGIN
            INCLUDE ~%MOD_FOLDER%/configurations/zstweaks_general_group_install.txt~
        END

        COPY "%MOD_FOLDER%/items/boneblade" ~override~
        COPY_EXISTING ~dagg14.itm~ ~override~
            LPF ALTER_ITEM INT_VAR id_desc=RESOLVE_STR_REF(@6000) END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=27,p1=40~ END
            LPF ADD_ON_HIT_EFF INT_VAR p2=4 tmg=1 STR_VAR res=~zstw_dg6~ effects=~op=12,dsize=4,dnum=1,p2=%op12_acid%,spec=1024;op=177,p1=%race_elf%;op=177,p1=%race_beholder%;op=177,p1=%race_drider%;op=177,p1=%race_hook_horror%;op=177,p1=%race_kuo_toa%;op=177,p1=%race_mind_flayer%;op=177,p1=%race_spider%;op=177,p1=%race_umberhulk%;op=177,p1=%race_sahuagin%~ END

            PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1440~ OR
                    (general_tweaks_installed AND zst_group_1440_dagger_boost)
            BEGIN
                READ_STRREF 0x54 desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*THAC0[ %TAB%]*:?[ %TAB%]*\)\+[0-9]~ ~\1+5~ //thac0
                END
                SAY_EVALUATED 0x54 ~%desc%~
            END

        // item upgrade patch for quietus
        ACTION_IF FILE_EXISTS_IN_GAME ~c2dagg01.itm~ BEGIN
            COPY "%MOD_FOLDER%/patches/item_upgrade/quietus" ~override~
            COPY_EXISTING ~c2dagg01.ITM~ ~override~
                LPF DELETE_EFFECT INT_VAR match_opcode = 263 END
                LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 74 END
                LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 142 END
                LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 139 match_parameter1 = 14674  END
                LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 15 probability1 = 89 probability2 = 0 END
                LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 19 probability1 = 29 probability2 = 0 END
                LPF ALTER_EFFECT INT_VAR match_probability2 = 50 probability1 = 100 probability2 = 0 END
                LPF ALTER_HEADER INT_VAR match_type = 1 dicesize = 4 dicenumber = 1 END

                PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1440~ OR (general_tweaks_installed AND zst_group_1440_dagger_boost) BEGIN
                    LPF ALTER_HEADER INT_VAR match_type = 1 damage = 5 to_hit = 6 END
                    LPF ALTER_EFFECT STR_VAR match_resource = ~zstwdpb4~ resource = ~zstwdpb5~ END
                    LPF ALTER_EFFECT STR_VAR match_resource = ~zstwdpt4~ resource = ~zstwdpt5~ END
                END ELSE BEGIN
                    LPF ALTER_HEADER INT_VAR match_type = 1 damage = 5 to_hit = 5 END
                END

                PATCH_IF zst_1450_allow_daggers AND
                         (MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1450~ OR
                         (general_tweaks_installed AND zst_group_1450_finesse))
                BEGIN
                    SET ench = 5
                    LPM PROBABILITY_BONUS
                    LPF ALTER_EFFECT INT_VAR probability1 =(bonus+base_dagg_dart) STR_VAR match_resource = ~zstwd4~ resource = ~zstwd5~  END
                END

                LPF ALTER_ITEM INT_VAR id_name = RESOLVE_STR_REF(@50111) id_desc=RESOLVE_STR_REF(@50110) ench=5 END
                LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=27,p1=50;op=301,p1=1,p2=1~ END

                LPF ADD_ON_HIT_EFF INT_VAR p2=4 tmg=1 ip=0 STR_VAR res=~zstw_dg7~ effects=~op=12,dsize=6,dnum=1,p2=%op12_acid%,spec=1024;op=177,p1=%race_elf%;op=177,p1=%race_beholder%;op=177,p1=%race_drider%;op=177,p1=%race_hook_horror%;op=177,p1=%race_kuo_toa%;op=177,p1=%race_mind_flayer%;op=177,p1=%race_spider%;op=177,p1=%race_umberhulk%;op=177,p1=%race_sahuagin%;op=326,ip=-1,res=zstw_dg8,p2=138,p1=4~ END

            BUT_ONLY

        END

    END

END
