ACTION_IF is_bg2ee_or_eet BEGIN
  COPY ~%MOD_FOLDER%/spells/divine/entangle~ ~override~
END

ACTION_IF NOT is_iwdee BEGIN

  COPY_EXISTING ~sw1h58.itm~  ~override~
                ~sw1h59.itm~  ~override~
                ~mound.itm~   ~override~
                ~spin688.spl~ ~override~
                ~bdbow06.spl~ ~override~
    LPF ALTER_EFFECT_EX INT_VAR match_opcode = 0 STR_VAR match_parameter1_ex = ~"-2"~ parameter1_ex = ~"-3"~ END
    LPF CLONE_EFFECT INT_VAR match_opcode = 0 opcode = 1 parameter1 = 1 parameter2 = 1 END
    LPF ALTER_EFFECT INT_VAR silent = 1 match_resist_dispel = 1 resist_dispel = 3 END
  BUT_ONLY IF_EXISTS

END

COPY_EXISTING ~sppr105.spl~ ~override~
              ~spwm111.spl~ ~override~
  LPF ALTER_SPELL INT_VAR desc=RESOLVE_STR_REF(@5100) END

  PATCH_IF NOT is_iwdee BEGIN
    LPF ALTER_EFFECT_EX INT_VAR match_opcode = 0 STR_VAR match_parameter1_ex = ~"-2"~ parameter1_ex = ~"-3"~ END
  END ELSE BEGIN
    LPF CLONE_EFFECT_EX INT_VAR match_opcode = 142 match_parameter2 = 144 parameter2 = 0 opcode = 0 STR_VAR parameter1_ex = ~"-3"~ END
  END

  LPF CLONE_EFFECT INT_VAR match_opcode = 0 opcode = 1 parameter2 = 1 parameter1 = 1 END
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_savingthrow = 1 savingthrow = 2 END

  PATCH_IF NOT is_iwdee AND NOT has_scs_spell_nerf BEGIN
    LPF ADD_SPL_TGTEFF INT_VAR t=99 ip=0 rd=0 p2=104 STR_VAR res=~%SOURCE_RES%~ effects=~op=318,p2=63;op=318,p2=13;op=318,p1=%race_dragon%;op=318,p1=%race_elemental%;op=318,p1=%race_shambling_mound%;op=318,p1=%race_treant%;op=318,p1=%race_slime%;op=318,p1=%race_giant%;op=318,p1=%race_shadow%;op=318,p1=%race_spectral_undead%;op=318,p1=%race_wraith%;op=318,p1=%race_specter%;op=318,p1=%race_mist%;op=318,p1=%race_will-o-wisp%;op=318,p1=%race_ankheg%;op=318,p1=%race_wyvern%;op=318,p1=%general_weapon%,p2=103;op=318,p1=%class_spectral_troll%,p2=105~ END
  END

ACTION_IF NOT is_iwdee BEGIN // saving throw evolution

  COPY_EXISTING ~sppr105.spl~ ~override~
    SET new_bonus = 2
    LPF ALTER_EFFECT INT_VAR savebonus = new_bonus END
    LPF ~CD_LEVEL_SELECT-O-MATIC~ INT_VAR step_dur = 0 dur_special = 1 min_abil=1 step_size=4 level_cap=5 END
    LPF ~CD_LEVEL_SELECT-O-MATIC~ INT_VAR step_dur = 0 dur_special = 1 min_abil=1 step_size=5 level_cap=20 END

    PATCH_FOR_EACH h IN 1 2 3 4 BEGIN
      LPF ALTER_EFFECT INT_VAR header = h savebonus = (new_bonus - h) END
    END

    LPF ALTER_EFFECT INT_VAR match_opcode = 318 savingthrow = 0 savebonus = 0 END

END
