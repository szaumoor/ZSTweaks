COPY ~ZSTweaks/items/dagger_boost~ ~override~
ADD_PROJECTILE ~ZSTweaks/pro/ZSTW_IND.PRO~

COPY_EXISTING_REGEXP "ZSTWDP08.spl" ~override~ // spells to insert projectiles to (critical dagger effect)
    LPF ALTER_HEADER INT_VAR match_type = 1 projectile = "%ZSTW_IND%" END
BUT_ONLY
COPY_EXISTING_REGEXP "ZSTWDP1[02468]\.spl" ~override~ // spells to insert projectiles to (throwing daggers and darts)
    LPF ALTER_HEADER INT_VAR match_type = 1 projectile = "%ZSTW_IND%" END
BUT_ONLY
COPY_EXISTING_REGEXP "ZSTWDP20.spl" ~override~ // spells to insert projectiles to (critical dagger effect)
    LPF ALTER_HEADER INT_VAR match_type = 1 projectile = "%ZSTW_IND%" END
BUT_ONLY

COPY_EXISTING "DAGG01.ITM" ~override~
    SPRINT str_sf @99998
    READ_STRREF 0x50 desc
    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)2~ ~\10~ // speed modification
    END
    SAY_EVALUATED 0x50 ~%desc%~
    LPF ITM_REF_UDESC_INDEX RET changed_description_index = result END
BUT_ONLY

COPY_EXISTING_REGEXP ".*\.itm" ~override~
    LPF ITM_CAT RET wpn_cat = result END
    LPF ITM_PROF RET wpn_prof = result END
    LPF ITM_ENCH RET ench = result END
	PATCH_IF wpn_cat = 16 AND wpn_prof = 96 BEGIN // daggers
        PATCH_IF "%SOURCE_RES%" STRING_COMPARE_CASE "dagg04" != 0 BEGIN // not grave binder (fake dagger) exceptions
            LPF WPN_THAC0 RET thac0 = result END
            LPF ALTER_HEADER INT_VAR silent = 1 match_type = 1 speed = 0 to_hit = thac0+1 END
            LPF ALTER_ITEM INT_VAR unid_ref_desc = changed_description_index END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=301,p1=1,p2=1~ END
            PATCH_MATCH ench
               WITH 0 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP08~ END
                END 1 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP10~ END
                END 2 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP12~ END
                END 3 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP14~ END
                END 4 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP16~ END
                END 5 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP18~ END
                END 6 BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=341,p2=1,res=ZSTWDP20~ END
                END DEFAULT
                    LPF ITM_STR_NAME RET name = result END
                    PATCH_WARN "[Dagger boost] Interesting, we found a weapon with more than +6 in enchantment: %SOURCE_RES%, %name%"
            END
            LPF ITM_REF_DESC RET description_index = result END
            PATCH_IF description_index > "-1" BEGIN
                READ_STRREF 0x54 desc
                SPRINT str_sf @99998
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\10~ // speed
                END
                SAY_EVALUATED 0x54 ~%desc%~
            END
        END
    END
BUT_ONLY
