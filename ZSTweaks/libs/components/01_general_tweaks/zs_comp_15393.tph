COPY_EXISTING_REGEXP ~.*\.ITM~ ~override~
    LPF ITM_CAT RET cat = result END
    PATCH_IF cat = 11 BEGIN
        LPF ITM_USABILITY0 RET usability_flags = result END
        PATCH_IF usability_flags > 0 BEGIN
            LPF IS_USABILITY_FLAG_SET INT_VAR flag=9 RET cleric_thief_flag = result END
            LPF IS_USABILITY_FLAG_SET INT_VAR flag=6 RET bard_flag = result END
            LPF IS_USABILITY_FLAG_SET INT_VAR flag=16 RET fighter_mage_thief = result END
            LPF IS_USABILITY_FLAG_SET INT_VAR flag=17 RET fighter_thief = result END
            LPF IS_USABILITY_FLAG_SET INT_VAR flag=19 RET mage_thief = result END
            LPF IS_USABILITY_FLAG_SET INT_VAR flag=22 RET thief = result END

            PATCH_IF cleric_thief_flag BEGIN
                WRITE_LONG 0x1e (THIS - 2**9)
            END

            PATCH_IF bard_flag BEGIN
                WRITE_LONG 0x1e (THIS - 2**6)
            END

            PATCH_IF fighter_mage_thief BEGIN
                WRITE_LONG 0x1e (THIS - 2**16)
            END

            PATCH_IF fighter_thief BEGIN
                WRITE_LONG 0x1e (THIS - 2**17)
            END

            PATCH_IF mage_thief BEGIN
                WRITE_LONG 0x1e (THIS - 2**19)
            END

            PATCH_IF thief BEGIN
                WRITE_LONG 0x1e (THIS - 2**22)
            END

            LPF ITM_MIN_INT RET min_int = result END
            PATCH_IF min_int != 0 BEGIN // skip letters and the like
                SET found = 0
                GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x38
                PHP_EACH ab_array AS int => ab_off BEGIN
                    GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
                    PHP_EACH fx_array AS int => fx_off BEGIN
                        SET opcode = SHORT_AT (fx_off)
                        PATCH_IF opcode = 146 OR opcode = 148 BEGIN
                            ++found
                            PATCH_IF found = 1 BEGIN
                                READ_ASCII ( fx_off + 0x14 ) resource
                                TO_UPPER resource
                                INNER_PATCH_FILE ~%resource%.SPL~ BEGIN
                                    READ_LONG 0x34 spell_level
                                    READ_SHORT 0x1c spell_type
                                END
                            END
                        END
                    END
                END

                PATCH_IF found = 1 AND spell_level > 0 AND spell_level <= 10 BEGIN // skip spells with invalid levels
                    PATCH_IF min_int > 0 BEGIN // skip items that don't have a minimum int requirement
                        PATCH_IF spell_type = 1 BEGIN // wizard spell
                            LPF ALTER_ITEM INT_VAR min_int = 8 + spell_level END
                        END ELSE PATCH_IF spell_type = 2 BEGIN // priest spell
                            LPF ALTER_ITEM INT_VAR min_wis = 8 + spell_level END
                        END
                    END
                END
            END
        END
    END
BUT_ONLY
