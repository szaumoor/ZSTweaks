ACTION_IF (VARIABLE_IS_SET ~zs_comp_3040~) BEGIN
  FAIL ~Script already included: zs_comp_3040~
END ELSE BEGIN
  OUTER_SET ~zs_comp_3040~ = 1
END

COPY ~%MOD_FOLDER%/creatures/planetar~ ~override~

COPY_EXISTING ~PLANETAR.ITM~ ~override~
  LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=189,p2=1~ END
  PATCH_IF MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~1541~ OR MOD_IS_INSTALLED ~ZSTweaks.tp2~ ~103~ BEGIN // stoneskin vorpal prot

    LPF DELETE_EFFECT STR_VAR match_resource = ~ZSTWVRPA~ END
    LPF ALTER_EFFECT INT_VAR match_probability1 = 24 probability1 = 19 END

  END ELSE BEGIN

    LPF DELETE_EFFECT INT_VAR match_opcode = 13 END // no vorpal effect
    LPF DELETE_EFFECT INT_VAR match_opcode = 141 match_parameter2 =  39 END // no vorpal effect
    LPF DELETE_EFFECT INT_VAR match_opcode = 61 END // no vorpal effect
    LPF DELETE_EFFECT INT_VAR match_opcode = 139 END // no vorpal effect

    PATCH_IF NOT has_eefixpack BEGIN
      LPF ALTER_EFFECT INT_VAR match_probability1 = 25 probability1 = 19 END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_probability1 = 24 probability1 = 19 END
    END

  END

  PATCH_IF NOT has_eefixpack BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 81 parameter2 = 0 END // bullshit fixed from deafness opcode
  END

  LPF ALTER_ITEM INT_VAR ench = 5 END
  LPF ALTER_HEADER INT_VAR to_hit = 5 END
  LPF GET_DMG_TYPE STR_VAR type = "fire" RET fire = result END
  LPF GET_DMG_TYPE STR_VAR type = "cold" RET cold = result END
  LPF ADD_ON_HIT_EFF INT_VAR p1=2 spec=1024 STR_VAR effects = ~op=12,p2=%fire%;op=12,p2=%cold%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 58 parameter1 = 25 parameter2 = 2 END

  LPF ADD_ON_HIT_EFF INT_VAR p2=104 STR_VAR effects = ~op=326,p1=%race_demonic%,res=ZSTWPLNT;op=326,p1=%race_planetar%,res=ZSTWPLNT;op=326,p1=%race_darkplanetar%,res=ZSTWPLNT;op=326,p1=%race_solar%,res=ZSTWPLNT;op=326,p1=%race_antisolar%,res=ZSTWPLNT;op=326,p2=1,res=ZSTWPLNT~ END

  LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=206,res=SPPR326;op=206,res=SPPR324;op=206,res=SPPR302;op=206,res=SPCL722;op=206,res=SPWI308;op=206,res=SPWI304;op=206,res=SPWI204;op=206,res=SPWI101;op=206,res=SPPR302;op=206,res=SPPR304;op=206,res=SPWI313;op=206,res=SPPR313;op=206,res=SPPR314;op=206,res=SPPR105;op=206,res=SPWI215;op=206,res=SPWI213;op=206,res=SPPR211;op=102,p1=1;op=102,p1=2;op=102,p1=3~ END // invulnerability to level 1-3 spells

COPY_EXISTING ~PLANGOOD.CRE~ ~override~
              ~PLANEVIL.CRE~ ~override~
  ADD_MEMORIZED_SPELL ~ZSTWPLTE~ #0 ~innate~ (1)

COPY_EXISTING ~ZSTWPLTE.SPL~ ~override~
  LPF ALTER_SPELL INT_VAR ref_name = RESOLVE_STR_REF(@8160) ref_desc = RESOLVE_STR_REF(@8161) END
