ACTION_IF (VARIABLE_IS_SET ~zs_comp_1197~) BEGIN
    FAIL ~Script already included: zs_comp_1197~
END ELSE BEGIN
    OUTER_SET ~zs_comp_1197~ = 1
END

ACTION_IF is_bg2ee_or_eet BEGIN

    ACTION_IF zst_1997_kuotoa BEGIN
        COPY_EXISTING ~MISCA7.itm~ ~override~ //kuo
            LPF ALTER_ITEM INT_VAR general_desc = RESOLVE_STR_REF(@8370) END
            LPF ADD_ITEM_HEADER INT_VAR target=5 charges=1 depletion=1 projectile=0 STR_VAR icon = ~IMISC8H~ END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 tmg=9 rd=0 p1=1 STR_VAR effects = ~op=0,p2=2;op=54;op=36~ END
   END

    ACTION_IF zst_1997_orb BEGIN
        COPY_EXISTING ~MISCA8.itm~ ~override~ //eye
            LPF ALTER_ITEM INT_VAR general_desc = RESOLVE_STR_REF(@8371) END
            LPF ADD_ITEM_HEADER INT_VAR target=5 charges=1 depletion=1 projectile=0 STR_VAR icon = ~IMISCA8~ END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 tmg=9 rd=0 p1=1 STR_VAR effects = ~op=0;op=33;op=35~ END
    END

    ACTION_IF zst_1997_brain BEGIN
        COPY_EXISTING ~MISCBV.itm~ ~override~ // brain
            LPF ALTER_ITEM INT_VAR general_desc = RESOLVE_STR_REF(@8372) END
            LPF ADD_ITEM_HEADER INT_VAR target=5 charges=1 depletion=1 projectile=0 STR_VAR icon = ~IMISC8H~ END
            LPF ADD_ON_HIT_EFF INT_VAR t=3 tgt=1 tmg=9 rd=0 p1=1 STR_VAR effects = ~op=191;op=191,p2=1;op=34;op=37~ END
    END

    ACTION_IF zst_1997_strohm BEGIN
        COPY_EXISTING ~KEY20.itm~ ~override~ // STROHM
            SET desc = RESOLVE_STR_REF(@8373)
            LPF ALTER_ITEM INT_VAR general_desc=desc id_desc=desc STR_VAR appearance="H0" END
            LPF ADD_ITEM_HEADER INT_VAR target=5 charges=1 depletion=3 projectile=0 flags=2048 STR_VAR icon = ~IKEY20~ END
            LPF ADD_ON_HIT_EFF STR_VAR effects = ~t=3,op=146,tgt=1,res=SPWI515~ END
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=91,p1=10;op=276,p1=10~ END
    END

    ACTION_IF zst_1997_shadow BEGIN
        COPY ~%MOD_FOLDER%/items/shadow_wardstone~ ~override~
        COPY_EXISTING ~MISC6P.itm~ ~override~ // wardstone
            SET desc = RESOLVE_STR_REF(@8374)
            LPF ALTER_ITEM INT_VAR general_desc=desc id_desc=desc general_name=8181 category=7 flags=33554508 END

            SET blinded_strref1 = 14674
            SET blinded_strref2 = 1474
            LPF ADD_EQUIPPED_EFF STR_VAR effects = ~op=275,p1=5;op=101,p2=74;op=169,p2=8;op=267,p1=%blinded_strref1%;op=267,p1=%blinded_strref2%~ END
            LPF ADD_EQUIPPED_EFF INT_VAR p2=9 STR_VAR effects = ~op=177,p1=%kit_shadowdancer%,res=ZSTWWDS0;op=177,p1=%kit_shadowdancer%,res=ZSTWWDS1;op=177,p1=%kit_shadowdancer%,res=ZSTWWDS2;op=177,p1=%kit_shadowdancer%,res=ZSTWWDS3;op=177,p1=%kit_dark_moon%,res=ZSTWWDS0;op=177,p1=%kit_dark_moon%,res=ZSTWWDS1;op=177,p1=%kit_dark_moon%,res=ZSTWWDS2;op=177,p1=%kit_dark_moon%,res=ZSTWWDS3~ END
            LPF ADD_EQUIPPED_EFF INT_VAR p2=4 STR_VAR effects = ~op=219,p1=%race_shadow%;op=219,p1=%race_spectral_undead%;op=219,p1=%race_specter%;op=219,p1=%race_wraith%~ END
            LPF ADD_EQUIPPED_EFF INT_VAR p2=5 STR_VAR effects = ~op=219,p1=%class_shadow_dragon%;op=219,p1=%class_spectral_troll%~ END

            PATCH_IF has_zs_shadowdancer_overhaul BEGIN
                LPF ADD_EQUIPPED_EFF INT_VAR p2=9 STR_VAR effects = ~op=177,p1=%kit_zs_shadowdancer%,res=ZSTWWDS0;op=177,p1=%kit_zs_shadowdancer%,res=ZSTWWDS1;op=177,p1=%kit_zs_shadowdancer%,res=ZSTWWDS2;op=177,p1=%kit_zs_shadowdancer%,res=ZSTWWDS3~ END
            END

            PATCH_IF MOD_IS_INSTALLED "ZSTweaks.tp2" "16640" OR MOD_IS_INSTALLED "ZSTweaks.tp2" "16642" BEGIN
                LPF ALTER_ITEM INT_VAR this_plus="-1" flags=2**25 END
            END
    END
END
