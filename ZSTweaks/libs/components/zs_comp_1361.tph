ACTION_IF (VARIABLE_IS_SET ~zs_comp_1361~) BEGIN
  FAIL ~Script already included: zs_comp_1361~
END ELSE BEGIN
  OUTER_SET ~zs_comp_1361~ = 1
END


ACTION_IF zst_1361_axes_crits OR zst_1361_halberds_crits BEGIN

    COPY ~%MOD_FOLDER%/items/ax_halb_crit~ ~override~
    COPY_EXISTING_REGEXP ~^.+\.itm$~ ~override~
        LPF ITM_CAT RET cat = result END
        LPF ITM_ABILITIES_NUM RET num_abilities = result END
        LPF ITM_APPEARANCE RET appearance = result END
        LPF ITM_ENCH RET ench = result END
        PATCH_IF ench < 7 AND num_abilities > 0 AND NOT ("%appearance%" STR_EQ "  ") BEGIN

            PATCH_IF zst_1361_axes_crits AND cat = 25 AND NOT "%SOURCE_RES%" STR_EQ "a7-ax08" // AXE OF THE JESTER (wares of the planes)
            BEGIN

                LPF IS_FLAG_SET INT_VAR flag = 1 RET is_two_handed = result END
                PATCH_IF is_two_handed BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects=~op=341,p2=1,res=ZSTWAX2%ench%,spec=1~ END
                END ELSE BEGIN
                    LPF ADD_EQUIPPED_EFF STR_VAR effects=~op=341,p2=1,res=ZSTWAXM%ench%,spec=1;op=341,p2=1,res=ZSTWAXR%ench%,spec=2~ END
                END

            END ELSE PATCH_IF zst_1361_halberds_crits AND cat = 30 BEGIN
                LPF ADD_EQUIPPED_EFF STR_VAR effects=~op=341,p2=1,res=ZSTWHLB%ench%,spec=1~ END
            END

        END

    BUT_ONLY

END
