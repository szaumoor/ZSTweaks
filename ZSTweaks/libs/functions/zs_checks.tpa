DEFINE_PATCH_FUNCTION CHECK_EXTENSION
INT_VAR
    fail_on_mismatch = 1
    suppress = 0
STR_VAR
    extension = ""
RET
    result
BEGIN
    PATCH_IF STRING_LENGTH "%extension%" = 0 BEGIN
        PATCH_FAIL "Extension is empty"
    END
    SET last_index = (STRING_LENGTH "%SOURCE_RES%") + 1
    LPF SUBSTRING INT_VAR start = last_index length = 3 STR_VAR string = "%SOURCE_FILE%" RET substring END
    PATCH_IF NOT "%substring%" STR_EQ "%extension%" BEGIN
        result = 0
        PATCH_IF suppress = 0 BEGIN
            PATCH_IF fail_on_mismatch = 1 BEGIN
                PATCH_FAIL "This function only works for .%extension% files"
            END ELSE BEGIN
                PATCH_WARN "This function only works for .%extension% files"
            END
        END
    END ELSE BEGIN
        result = 1
    END
END


DEFINE_DIMORPHIC_FUNCTION ~IS_BIT_SET~
INT_VAR
    bit       = "-1"
    bit_field = "-1"
    bit_limit = 32
RET
    result // 1 if flag is set, 0 if it's unset. -1 if invalid flag is asked for
BEGIN
    ACTION_IF bit_field = "-1" BEGIN
        FAIL "Bit field not set"
    END
    ACTION_IF bit >= 0 AND bit < bit_limit BEGIN
        OUTER_SET bitmask = (1 << bit)
        OUTER_SET result = (bit_field & bitmask) != 0
    END ELSE BEGIN
        OUTER_SET result = "-1"
    END
END

DEFINE_PATCH_FUNCTION IS_FLAG_SET
INT_VAR
    flag = "-1"
RET
    result // 1 if flag is set, 0 if it's unset. -1 if invalid flag is asked for
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "ITM" END
    LPF ITM_FLAGS RET itm_flags = result END
    LPF IS_BIT_SET INT_VAR bit = flag bit_field = itm_flags RET set = result END
    result = set
END

DEFINE_PATCH_FUNCTION IS_USABILITY_FLAG_SET
INT_VAR
    flag = "-1"
RET
    result // 1 if flag is set, 0 if it's unset. -1 if invalid flag is asked for
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "ITM" END
    LPF ITM_USABILITY0 RET itm_flags = result END
    LPF IS_BIT_SET INT_VAR bit = flag bit_field = itm_flags RET set = result END
    result = set
END

DEFINE_PATCH_FUNCTION CRE_IS_FLAG_SET
INT_VAR
    flag = "-1"
RET
    result
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    LPF CRE_FLAGS RET cre_flags = result END
    LPF IS_BIT_SET INT_VAR bit = flag bit_field = cre_flags RET set = result END
    result = set
END
