// This function tries to find the last valid header of a spell or item ability in a given file. A "valid" header is defined
// as either the last header in a sequence of headers or a header that represents a "gap" in the sequence.

DEFINE_PATCH_FUNCTION LAST_VALID_HEADER
INT_VAR
    type = "-1"
    level_to_avoid = 100
RET
    result
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "SPL" END
    READ_LONG 0x64 ability_offset
    READ_SHORT 0x68 num_abilities

    PATCH_IF num_abilities > 1 BEGIN // #1 if there's at least more than one header to look at...
        READ_SHORT (ability_offset + 0x10) first_casting_level // first header casting level
        READ_SHORT (ability_offset + 0x28 + 0x10) next_casting_level // first scaling header (i.e. second one)
        PATCH_IF next_casting_level < level_to_avoid BEGIN // #2 if the diff between both is one, let's continue...
           // PATCH_IF num_abilities > 2 BEGIN // #3 if we have more headers to examine, let's continue...
                SET found = 0
                FOR ( h = 2; h < num_abilities; ++h ) BEGIN
                    SET new_offset = ability_offset + (0x28 * h) + 0x10
                    READ_SHORT new_offset curr_casting_level
                    SET diff = curr_casting_level - next_casting_level
                    PATCH_IF diff > 1 BEGIN // #4 if the diff is more than one, we found it
                        result = h - 1
                        h = num_abilities + 1 // i.e. break
                        found = 1
                    END
                    next_casting_level = curr_casting_level
                END
                PATCH_IF found = 0 BEGIN
                    result = num_abilities - 1
                END
        END ELSE BEGIN // #2 otherwise, the last valid header in question is the previous one: the first header
            result = 0
        END
    END ELSE BEGIN // #1 otherwise, the last valid header will be the first one, or none if none are present
        result = num_abilities - 1
    END
END


DEFINE_PATCH_FUNCTION ITM_LAST_HEADER_TYPE
INT_VAR
    type = 1
RET
    result
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "ITM" END
    READ_LONG 0x64 ability_offset
    READ_SHORT 0x68 num_abilities

    PATCH_IF num_abilities > 1 BEGIN
        SET bytes = 0x38
        SET header = 0
        FOR ( h = 1; h < num_abilities; ++h ) BEGIN
            ability_offset += bytes
            SET t = BYTE_AT (ability_offset)
            PATCH_IF t = type BEGIN
                header = h
            END
        END
        result = header
    END ELSE BEGIN
        SET t = BYTE_AT (ability_offset)
        PATCH_IF t = type BEGIN
            result = 0
        END ELSE BEGIN
            result = "-1"
        END
    END
END

// This function tries to see if an opcode (effect) is present in a given creature file.
DEFINE_PATCH_FUNCTION CRE_OP_PRESENT
INT_VAR
    opcode1 = "-1"
    opcode2 = "-1"
RET
    result
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    READ_LONG 0x2c4 eff_offset
    READ_LONG 0x2c8 eff_length
    READ_BYTE 0x33 eff_type
    PATCH_IF eff_type = 0 BEGIN
        SET effect_size = 0x30
    END ELSE BEGIN
        SET effect_size = 0x108
    END
    SET found = 0
    FOR ( index = 0; index < eff_length; ++index ) BEGIN
        PATCH_IF eff_type = 0 BEGIN
            READ_SHORT eff_offset op
        END ELSE BEGIN
            READ_LONG (eff_offset + 0x08) op
        END
        PATCH_IF op = opcode1 OR op = opcode2 BEGIN
            found = 1
            index = eff_length // break
        END
        eff_offset += effect_size
    END
    result = found
END


