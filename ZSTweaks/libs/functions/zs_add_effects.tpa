/**
 * Adds an effect on-equipped to an item. Uses Argent77's a7_auto_apply_spl_effect function.
 */
DEFINE_PATCH_FUNCTION ADD_EQUIPPED_EFF
INT_VAR
    p1   =   0 // parameter 1
    p2   =   0 // parameter 2
    ip   = "-1" // insert point
    spec =   0 // special
STR_VAR
    res     = ~~
    effects = ~~ // effect codes
BEGIN
    PATCH_IF zs_debug BEGIN
        LPF CHECK_EXTENSION STR_VAR extension = "ITM" END
    END

    LPF a7_auto_apply_spl_effect
        INT_VAR
            def_target      = 1
            def_timing      = 2
            def_parameter1  = p1
            def_parameter2  = p2
            def_insertpoint = ip
            def_special     = spec
        STR_VAR
            def_resource  = ~%res%~
            function_name = ~ADD_ITEM_EQEFFECT~
            effect_codes  = ~%effects%~
    END
END

DEFINE_PATCH_FUNCTION ADD_CRE_PASSIVE
INT_VAR
    op = 0
    p1 = 0
    p2 = 0
    tmg = 9
    tgt = 1
STR_VAR
    res = ""
BEGIN
    PATCH_IF zs_debug BEGIN
        LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    END

    SET opcode = op
    SET target = tgt
    SET timing = tmg
    SET power = 0
    SET parameter1 = p1
    SET parameter2 = p2
    SET resist_dispel = 0
    SET duration = 0
    SET probability1 = 100
    SET probability2 = 0
    SPRINT resource ~%res%~
    SET dicenumber = 0
    SET dicesize = 0
    SET savingthrow = 0
    SET savebonus = 0
    SET school = 0
    SET lowestafflvl = 0
    SET highestafflvl = 0
    SET parameter3 = 0
    SET parameter4 = 0
    SPRINT vvcresource ~~
    SPRINT resource2 ~~
    SET casterx = 0 - 1
    SET castery = 0 - 1
    SET targetx = 0 - 1
    SET targety = 0 - 1
    SET restype = 0
    SPRINT effsource ~~
    SET sourceslot = "-1"
    SPRINT effvar ~~
    SET casterlvl = 0
    SET sectype = 0


    LPM ADD_CRE_EFFECT
END

DEFINE_PATCH_MACRO ADD_CRE_MELEE_EFFECT 
BEGIN
    LPF ADD_CRE_PASSIVE INT_VAR op = 248 STR_VAR res = "%melee_effect%" END
END

DEFINE_PATCH_MACRO ADD_CRE_RANGED_EFFECT 
BEGIN
    LPF ADD_CRE_PASSIVE INT_VAR op = 249 STR_VAR res = "%ranged_effect%" END
END

DEFINE_PATCH_MACRO ADD_CRE_MOVEMENT_BOOST
BEGIN
    LPF ADD_CRE_PASSIVE INT_VAR op = 176 p1 = movement_boost END
END

DEFINE_PATCH_MACRO ADD_CRE_CRIT_BOOST
BEGIN
    LPF ADD_CRE_PASSIVE INT_VAR op = 301 p1 = 1 END
END

DEFINE_PATCH_FUNCTION ADD_ON_HIT_EFF
INT_VAR
    tgt    = 2
    tmg    = 0
    pwr    = 0
    p1     = 0
    p2     = 0
    rd     = 0
    dur    = 0
    pro1   = 100
    pro2   = 0
    dnum   = 0
    dsize  = 0
    stype  = 0
    sbonus = 0
    spec   = 0
    ip     = "-1"
    t      = 1
STR_VAR
    res     = ~~
    effects = ~~
BEGIN
    PATCH_IF zs_debug BEGIN
        LPF CHECK_EXTENSION STR_VAR extension = "ITM" END
    END

    LPF a7_auto_apply_spl_effect
        INT_VAR
            def_target        = tgt
            def_timing        = tmg
            def_power         = pwr
            def_parameter1    = p1
            def_parameter2    = p2
            def_resist_dispel = rd
            def_duration      = dur
            def_probability1  = pro1
            def_probability2  = pro2
            def_dicenumber    = dnum
            def_dicesize      = dsize
            def_savetype      = stype
            def_savebonus     = sbonus
            def_special       = spec
            def_insertpoint   = ip
            def_type          = t
        STR_VAR
            def_resource  = ~%res%~
            function_name = ~ADD_ITEM_EFFECT~
            effect_codes  = ~%effects%~
    END
END

DEFINE_PATCH_FUNCTION ADD_SLEEP_SAFE_DMG // untested; always preset target for now
INT_VAR
    p1       = 0
    p2       = "-1"
    dnum     = 0
    dsize    = 0
    t        = 1
    ip       = "-1"
STR_VAR
    type = 1
BEGIN
    PATCH_IF zs_debug BEGIN
        LPF CHECK_EXTENSION STR_VAR extension = "ITM" END
    END
    LPF ADD_ON_HIT_EFF STR_VAR effects = EVAL ~ip=%ip%,t=%t%,op=12,p1=%p1%,p2=%p2%,dnum=%dnum%,dsize=%dsize%,spec=1024~ END
END

DEFINE_PATCH_FUNCTION ADD_SPL_SELFEFF
INT_VAR
    p1  = 0
    p2  = 0
    t   = 1
    dur = 0
    rd  = 1
    pwr = 1
    ip  = "-1"
    stype = 0
    sbonus = 0
STR_VAR
    effects = ""
BEGIN
    PATCH_IF zs_debug BEGIN
        LPF CHECK_EXTENSION STR_VAR extension = "SPL" END
    END

    LPF a7_auto_apply_spl_effect
        INT_VAR
            def_target        = 1
            def_timing        = 0
            def_parameter1    = p1
            def_parameter2    = p2
            def_duration      = dur
            def_resist_dispel = rd
            def_power         = pwr
            def_type          = t
            def_insertpoint   = ip
            def_savetype      = stype
            def_savebonus     = sbonus
        STR_VAR
            function_name = ~ADD_SPELL_EFFECT~
            effect_codes  = ~%effects%~
    END
END

DEFINE_PATCH_FUNCTION ADD_SPL_TGTEFF
INT_VAR
    p1     = 0
    p2     = 0
    t      = 1
    tmg    = 0
    dur    = 0
    rd     = 1
    pwr    = 1
    ip     = "-1"
    stype  = 0
    sbonus = 0
    tgt    = 2
STR_VAR
    res     = ""
    effects = ""
BEGIN
    PATCH_IF zs_debug BEGIN
        LPF CHECK_EXTENSION STR_VAR extension = "SPL" END
    END

    LPF a7_auto_apply_spl_effect
        INT_VAR
            def_target        = tgt
            def_timing        = tmg
            def_parameter1    = p1
            def_parameter2    = p2
            def_duration      = dur
            def_resist_dispel = rd
            def_power         = pwr
            def_type          = t
            def_insertpoint   = ip
            def_savetype      = stype
            def_savebonus     = sbonus
        STR_VAR
            def_resource  = ~%res%~
            function_name = ~ADD_SPELL_EFFECT~
            effect_codes  = ~%effects%~
    END
END
