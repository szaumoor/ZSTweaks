ACTION_IF !VARIABLE_IS_SET ~zs_prefixes~ BEGIN
    WITH_SCOPE BEGIN
        PRINT "[[FIX]]: Setting invisible stalker enemies as elementals of the Elemental Plane of Air instead of spectres"        COPY_EXISTING ~STALKE.CRE~   ~override~
                      ~SENSTALK.CRE~ ~override~
            WRITE_BYTE 0x272 race_elemental
            WRITE_BYTE 0x273 class_elemental_air
        BUT_ONLY IF_EXISTS
    END

    ACTION_IF zst_gender_fix BEGIN
        WITH_SCOPE BEGIN
            PRINT "[[FIX]]: Setting many types of sexless creatures as niether-gendered if they're set to male or female so it doesn't intrude with opcodes that target male/female"
            OUTER_SET matches = 0
            COPY_EXISTING_REGEXP ~^.+\.cre$~ ~override~
                SET general = BYTE_AT(0x271)
                SET race    = BYTE_AT(0x272)
                PATCH_IF race    = race_golem              OR
                         race    = race_treant             OR
                         race    = race_statue             OR
                         race    = race_demonic            OR
                         race    = race_mind_flayer        OR
                         race    = race_mist               OR
                         race    = race_planetar           OR
                         race    = race_darkplanetar       OR
                         race    = race_solar              OR
                         race    = race_antisolar          OR
                         race    = race_beholder           OR
                         race    = race_genie              OR
                         race    = race_slime              OR
                         race    = race_mimic              OR
                         race    = race_doppleganger       OR
                         race    = race_myconid            OR
                         race    = race_shadow             OR
                         race    = race_wraith             OR
                         race    = race_specter            OR
                         race    = race_spectral_undead    OR
                         race    = race_elemental          OR
                         race    = race_shambling_mound    OR
                         race    = race_shrieker           OR
                         race    = ~%race_will-o-wisp%~    OR
                         race    = race_otyugh             OR
                         general = general_weapon
                BEGIN
                    SET gender = BYTE_AT(0x275)
                    PATCH_IF gender = gender_male OR gender = gender_female BEGIN
                        ++matches
                        WRITE_BYTE 0x275 gender_niether
                    END
                END
            BUT_ONLY
            PRINT "[[FIX]]: Found %matches% matches that were set to 'niether' gender"
        END
    END

    ACTION_IF is_iwdee BEGIN
        ACTION_IF has_eefixpack BEGIN
            PRINT ~[[FIX]]: Fixing broken BONECIR.spl~
            COPY_EXISTING ~#BONECIR.spl~ ~override~
                WRITE_ASCII 0x0 "SPL " (4)
        END
    END

    ACTION_IF is_bg2ee_or_eet BEGIN
        PRINT "[[FIX]]: Fixing incorrect speed factor in Lilarcor from 8 to 7"
        COPY_EXISTING ~SW2H14.ITM~ ~override~
            LPF ALTER_HEADER INT_VAR silent = 1 match_type = 1 speed = 7 END
            READ_STRREF 0x54 desc
            SPRINT str_sf @99998
            INNER_PATCH_SAVE new_description ~%new_description%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)8~ ~\17~
            END
            SAY_EVALUATED 0x54 ~%desc%~

        PRINT ~[[FIX]]: Non-standard scimitar/wakizashi/ninjato animations~
        COPY_EXISTING ~misc9q.itm~ ~override~
            WRITE_ASCII 0x22 "SC" #2

        COPY_EXISTING ~sw1h66.itm~ ~override~
            WRITE_ASCII 0x22 "SS" #2

        COPY_EXISTING ~sw1h67.itm~ ~override~
            WRITE_ASCII 0x22 "S1" #2

        COPY_EXISTING ~sw1h68a.itm~ ~override~
            WRITE_ASCII 0x22 "SC" #2

        PRINT "[[FIX]]: Misaligned index in hamm06.itm (Dwarven Thrower +2)"
        COPY_EXISTING ~hamm06.itm~ ~override~ // Dwarven Thrower +3
            LPF FJ_SPL_ITM_REINDEX END

        PRINT "[[FIX]]: Aesgareth enemies he fights during the card game being considered 'weapon' instead of 'humanoid', and a couple of others"
        COPY_EXISTING ~gordeck1.cre~ ~override~
                      ~gordeck2.cre~ ~override~
                      ~gordeck3.cre~ ~override~
                      ~gordeck4.cre~ ~override~
                      ~ohnavrum.cre~ ~override~
            PATCH_IF BYTE_AT(0x271) = general_weapon BEGIN
                WRITE_BYTE 0x271 general_humanoid
            END
        BUT_ONLY

        PRINT "[[FIX]]: Guardian of Air (ToB) should be set as an invisible stalker, not a human"
        COPY_EXISTING ~GORSTALK.cre~ ~override~
            WRITE_BYTE 0x272 race_elemental
            WRITE_BYTE 0x273 class_elemental_air

        PRINT "[[FIX]]: Thieves using bastard swords in BG2EE without being able to use them properly or illegally as per the rules of thieves. Replaced with short swords."
        COPY_EXISTING ~ARNTRA06.CRE~ ~override~
                      ~BMUGG1.CRE~   ~override~
                      ~GENTHF01.CRE~ ~override~
                      ~PIRPIR05.CRE~ ~override~
            LPF IS_WEAPON_EQUIPPED INT_VAR at_slot = 1 STR_VAR weapon = ~sw1h01~ RET found END
            PATCH_IF found BEGIN
                REPLACE_CRE_ITEM ~SW1H07~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
            END

        PRINT "[[FIX]]: Cambions that are set as 'thieves' wielding bastard swords without proficiency for it. Adding large sword proficiency 1 to them"
        COPY_EXISTING ~CAMBION1.CRE~ ~override~
                      ~TELCAM1.CRE~  ~override~
            PATCH_IF BYTE_AT(0x6e) = 0 BEGIN // if they have no proficiency in large swords
                WRITE_BYTE 0x6f 1 // add one pip to large sword proficiency
            END

        PRINT "[[FIX]]: Thieves using daggers without being proficient in them. Adding one pip to proficiency in daggers for them"
        COPY_EXISTING ~AR18THIF.CRE~ ~override~
                      ~ARNMAN09.CRE~ ~override~
                      ~BEELOO.CRE~   ~override~
                      ~BSHOP01.CRE~  ~override~
                      ~DRIM01.CRE~   ~override~
                      ~GENTH02.CRE~  ~override~
                      ~GENTMG01.CRE~ ~override~
                      ~SHTH05.CRE~   ~override~
            LPF DELETE_EFFECT INT_VAR silent = 1 match_pcode = 233 match_parameter1 = 1 match_parameter2 = 96 END
            LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 96 timing=9 target=1 END

        PRINT "[[FIX]]: Thieves using short swords without being proficient in them. Adding one pip to proficiency in short swords for them"
        COPY_EXISTING ~DSHOP01.CRE~ ~override~
                      ~GENTH01.CRE~  ~override~
                      ~GPTHIEF1.CRE~ ~override~
                      ~IRETHF03.CRE~ ~override~
                      ~ISHCLO01.CRE~ ~override~
                      ~ISHTHF01.CRE~ ~override~
                      ~PARTHF.CRE~   ~override~
                      ~UDDOOR08.CRE~ ~override~
                      ~UDDOOR09.CRE~ ~override~
                      ~WAYLANE.CRE~  ~override~
                      ~SARTHF01.CRE~ ~override~
                      ~IRETHF04.CRE~ ~override~
                      ~ISHTHF03.CRE~ ~override~
            LPF DELETE_EFFECT INT_VAR silent = 1 match_pcode = 233 match_parameter1 = 1 match_parameter2 = 91 END
            LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 91 timing=9 target=1 END

        PRINT "[[FIX]]: Thief ('Trainee') using a long sword without being proficient in them. Adding one pip to proficiency in long swords for them"
        COPY_EXISTING ~ARNTRA01.CRE~ ~override~ // needs to avoid duplication of this effect
            LPF DELETE_EFFECT INT_VAR silent = 1 match_pcode = 233 match_parameter1 = 1 match_parameter2 = 90 END
            LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 90 timing=9 target=1 END

    END

    ACTION_IF is_bgee_or_eet BEGIN
        PRINT "[[FIX]]: Fixing Riggilo having 4 pips in big swords but none in small swords while being forced to use small swords. Setting him to have 1 pip in small swords too."
        COPY_EXISTING ~RIGGILO.CRE~ ~override~
            PATCH_IF BYTE_AT(0x6e) = 0 BEGIN
                WRITE_BYTE 0x6f 1
            END
    END

    ACTION_IF has_eefixpack = 0 BEGIN
        ACTION_IF is_bgee_bg2ee_or_eet BEGIN
            PRINT "[[FIX]]: Removing bizarre & unused spell selection from shadow creatures that can be exploitable by the player"
        END

        ACTION_IF is_bgee_or_eet BEGIN
            COPY_EXISTING   ~bdwrai01.cre~ ~override~ // wraith
                            ~bdwrait1.cre~ ~override~ // wraith
                            ~bpshadow.cre~ ~override~ // shadow
                            ~bpwrai01.cre~ ~override~ // wraith
                            ~daitel.cre~   ~override~ // ghost
                            ~mistpo01.cre~ ~override~ // poison mist
                            ~mistposu.cre~ ~override~ // poison mist
                            ~shadfi02.cre~ ~override~ // devil shade
                            ~shadowsu.cre~ ~override~ // shadow
                            ~slaysh01.cre~ ~override~ // slayer shadow
                REMOVE_MEMORIZED_SPELL ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
                REMOVE_KNOWN_SPELL     ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
                BUT_ONLY IF_EXISTS

        END ELSE ACTION_IF is_bg2ee_or_eet BEGIN // bg2ee
            COPY_EXISTING   ~chwraith.cre~ ~override~ // child spirit
                            ~firwra01.cre~ ~override~ // greater wraith
                            ~fsghos.cre~   ~override~ // ghost
                            ~fsghos2.cre~  ~override~ // ghost
                            ~gormisti.cre~ ~override~ // ice mist
                            ~gormistp.cre~ ~override~ // poison mist
                            ~hgmis01.cre~  ~override~ // vampiric wraith
                            ~hgmis02.cre~  ~override~ // swamp horror
                            ~hgwra01.cre~  ~override~ // gorion (casts all spells via forcespell)
                            ~hgwra02.cre~  ~override~ // devil shade
                            ~hgwra03.cre~  ~override~ // devil shade
                            ~kshadow.cre~  ~override~ // shadow
                            ~mistcd01.cre~ ~override~ // crimson death
                            ~mistho01.cre~ ~override~ // mist horror
                            ~mistpo01.cre~ ~override~ // poison mist
                            ~mistposu.cre~ ~override~ // poison mist
                            ~mistva01.cre~ ~override~ // vampiric mist
                            ~mistwa01.cre~ ~override~ // wandering horror
                            ~ohhburic.cre~ ~override~ // burich
                            ~ohhshad.cre~  ~override~ // shadow
                            ~ohhshfi1.cre~ ~override~ // shadow fiend
                            ~ohhshfi2.cre~ ~override~ // devil shade
                            ~rngsha.cre~   ~override~ // shadow
                            ~rngsha01.cre~ ~override~ // shadow
                            ~rngsha02.cre~ ~override~ // shadow patrick - knows cure medium wounds and has generic priest script to use it, but doesn't have it memorized
                            ~rngsha03.cre~ ~override~ // shadow
                            ~rngsha04.cre~ ~override~ // shadow jailor
                            ~rngsha2d.cre~ ~override~ // shadow patrick
                            ~sarspir.cre~  ~override~ // prison spirit
                            ~sdshadfi.cre~ ~override~ // shadow fiend
                            ~sdshadow.cre~ ~override~ // shadow
                            ~sewsha01.cre~ ~override~ // shadow fiend
                            ~sewsha02.cre~ ~override~ // shadow fiend
                            ~sewsha03.cre~ ~override~ // shadow
                            ~shadfi01.cre~ ~override~ // shadow fiend
                            ~shadfi02.cre~ ~override~ // devil shade
                            ~shadow01.cre~ ~override~ // shadow
                            ~shadowsu.cre~ ~override~ // shadow
                            ~slaysh01.cre~ ~override~ // slayer shadow
                            ~spectr01.cre~ ~override~ // specter
                            ~spellh01.cre~ ~override~ // spellhaunt
                            //~tanwiz1.cre~  ~override~ // vellin dahn
                            ~telslav.cre~  ~override~ // slave wraith
                            ~telwrai.cre~  ~override~ // demon wraith
                            ~uhcreat.cre~  ~override~ // shadow
                            ~wraith01.cre~ ~override~ // wraith
                            ~wrashi01.cre~ ~override~ // shimmering wraith
                REMOVE_MEMORIZED_SPELL ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
                REMOVE_KNOWN_SPELL ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
            BUT_ONLY IF_EXISTS
        END
    END

    ACTION_IF is_bg2ee_or_eet BEGIN
        COPY_EXISTING ~DAGG24.itm~ ~override~
            LPF ITM_WEIGHT RET result END
            PATCH_IF result = 0 BEGIN
                PATCH_PRINT "[[FIX]]: Fixing DAGG24.itm (Dagger +3) weight from 0 to 1"
                LPF ALTER_ITEM INT_VAR weight = 1 END
                SPRINT str @99990
                READ_STRREF 0x54 desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\11~
                END
                SAY_EVALUATED 0x54 ~%desc%~
            END

        COPY_EXISTING ~SHAKTI1.itm~ ~override~
            PATCH_PRINT "[[FIX]]: Fixing SHAKTI1.itm (Dagger +3) speed factor from 1 to 0"
            LPF ALTER_HEADER INT_VAR speed = 0 END

        COPY_EXISTING ~MISC4U.itm~ ~override~
            LPF WPN_DICESIZE RET dicesize = result END
            PATCH_IF dicesize = 6 BEGIN
                PATCH_PRINT "[[FIX]]: Fixing MISC4U.itm (Embarl's Dagger) dicesize from 6 to 4"
                LPF ALTER_ITEM INT_VAR lore_id = 0 END
                LPF ALTER_HEADER INT_VAR silent = 1 dicesize = 4 END
                SPRINT str @99999
                READ_STRREF 0x54 desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)1[Dd]6~ ~\11d4~
                END
                SAY_EVALUATED 0x54 ~%desc%~
            END

        COPY_EXISTING ~MISC4Q.itm~ ~override~
            LPF WPN_DICENUM RET dicenum = result END
            LPF WPN_DICESIZE RET dicesize = result END
            PATCH_IF dicesize = 4 AND dicenum = 2 BEGIN
                PATCH_PRINT "[[FIX]]: Fixing MISC4Q.itm (The Ogre's Sword) so it uses bastard sword proficiency"
                LPF ALTER_ITEM INT_VAR lore_id = 0 wpn_prof = 89 END
                LPF ALTER_HEADER INT_VAR silent = 1 dicesize = 8 dicenumber = 1 END
                SPRINT str @99980
                SPRINT ls_str @99981
                SPRINT bs_str @99982
                READ_STRREF 0x50 desc
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                    REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str%[ %TAB%]*:?[ %TAB%]*\)%ls_str%~ ~\1%bs_str%~
                END
                SAY_EVALUATED 0x50 ~%desc%~
            END

         COPY_EXISTING "SW1H28.itm" ~override~
            PATCH_PRINT "[[FIX]]: SW1H28.itm (Cutthroat +4) so it has a speed factor of 0"
            LPF ALTER_HEADER INT_VAR speed = 0 END
            READ_STRREF 0x54 desc
            SPRINT str_sf @99998
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                REPLACE_TEXTUALLY ~\([%LNL%%MNL%%WNL%]+[ %TAB%]*%str_sf%[ %TAB%]*:?[ %TAB%]*\)[0-9]~ ~\10~
            END
            SAY_EVALUATED 0x54 ~%desc%~
    END

    ACTION_IF FILE_EXISTS_IN_GAME ~DOBONE.ITM~ BEGIN
        PRINT "[[FIX]]: Fixing DOBONE.ITM (Darts of Bone) to have a speed factor of 0 (+3 Darts)"
        COPY_EXISTING ~DOBONE.ITM~ ~override~
            LPF ALTER_HEADER INT_VAR speed = 0 END
    END

    ACTION_IF spell_cause_disease BEGIN
        PRINT "[[FIX]]: Cause Disease using Abjuration animations, instead of Necromancy ones"
        LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_CAUSE_DISEASE" RET spell_res END
        COPY_EXISTING ~%spell_res%~ ~override~
            WRITE_ASCII 0x10 "CAS_P07" #8
            WRITE_SHORT 0x22 9
            WRITE_BYTE 0x25 7
    END

    OUTER_SET ~zs_prefixes~ = 1
END
