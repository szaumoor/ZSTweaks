ACTION_IF !VARIABLE_IS_SET ~zs_preinstall_check~ BEGIN

    OUTER_SET is_bgee = GAME_IS ~bgee~
    OUTER_SET is_bg2ee = GAME_IS ~bg2ee~
    OUTER_SET is_iwdee = GAME_IS ~iwdee~
    OUTER_SET is_eet = GAME_IS ~eet~
    OUTER_SET includes_sod = GAME_INCLUDES "sod"
    OUTER_SET is_bgee_with_sod = is_bgee AND includes_sod

    ACTION_IF is_bgee BEGIN

        ACTION_IF includes_sod BEGIN
            PRINT "Baldur's Gate: Enhanced Edition with Siege of Dragonspear detected."
        END ELSE BEGIN
            PRINT "Baldur's Gate: Enhanced Edition without Siege of Dragonspear detected."
        END

    END ELSE ACTION_IF is_bg2ee BEGIN

        PRINT "Baldur's Gate II: Enhanced Edition detected."

    END ELSE ACTION_IF is_iwdee BEGIN

        PRINT "Icewind Dale: Enhanced Edition detected."

    END ELSE ACTION_IF is_eet BEGIN

        PRINT "Enhanced Edition Trilogy detected."

    END

    OUTER_SET is_bg2ee_or_eet      = is_bg2ee OR is_eet
    OUTER_SET is_bgee_or_eet       = is_bgee  OR is_eet
    OUTER_SET is_bgee_or_iwdee     = is_bgee  OR is_iwdee
    OUTER_SET is_bgee_bg2ee_or_eet = is_bgee  OR is_bg2ee OR is_eet

    OUTER_SET has_eefixpack = MOD_IS_INSTALLED "eefixpack.tp2" "0"

    ACTION_IF has_eefixpack BEGIN
        PRINT "EEFixpack detected."
    END

    OUTER_SET has_iwdification_divine   = MOD_IS_INSTALLED "setup-iwdification.tp2" "40"
    OUTER_SET has_scs_iwd_spells_divine = MOD_IS_INSTALLED "setup-stratagems.tp2"   "1510"
    OUTER_SET has_tof_iwd_spells_divine = MOD_IS_INSTALLED "dw_talents.tp2"         "1510"
    OUTER_SET has_iwd_spells_divine     = has_iwdification_divine OR has_scs_iwd_spells_divine OR has_tof_iwd_spells_divine

    ACTION_IF has_iwd_spells_divine BEGIN
        PRINT "IWD spells for divine classes detected."
        ACTION_IF has_scs_iwd_spells_divine BEGIN
            PRINT "IWD spells for divine classes installed through SCS."
        END ELSE ACTION_IF has_tof_iwd_spells_divine BEGIN
            PRINT "IWD spells for divine classes installed through ToF."
        END ELSE ACTION_IF has_iwd_spells_divine BEGIN
            PRINT "IWD spells for divine classes installed through IWDification."
        END
    END

    OUTER_SET has_iwdification_arcane   = MOD_IS_INSTALLED "setup-iwdification.tp2" "30"
    OUTER_SET has_scs_iwd_spells_arcane = MOD_IS_INSTALLED "setup-stratagems.tp2"   "1500"
    OUTER_SET has_tof_iwd_spells_arcane = MOD_IS_INSTALLED "dw_talents.tp2"         "1500"
    OUTER_SET has_iwd_spells_arcane     = has_iwdification_arcane OR has_scs_iwd_spells_arcane OR has_tof_iwd_spells_arcane

    ACTION_IF has_iwd_spells_arcane BEGIN
        PRINT "IWD spells for arcane classes detected."
        ACTION_IF has_scs_iwd_spells_arcane BEGIN
            PRINT "IWD spells for arcane classes installed through SCS."
        END ELSE ACTION_IF has_tof_iwd_spells_arcane BEGIN
            PRINT "IWD spells for arcane classes installed through ToF."
        END ELSE ACTION_IF has_iwd_spells_arcane BEGIN
            PRINT "IWD spells for arcane classes installed through IWDification."
        END
    END

    OUTER_SET has_zs_shadowdancer_overhaul = MOD_IS_INSTALLED "ZS_ShadowdancerOverhaul.tp2" "100"
    ACTION_IF has_zs_shadowdancer_overhaul BEGIN
        PRINT "ZS_ShadowdancerOverhaul detected."
        OUTER_SET kit_zs_shadowdancer = IDS_OF_SYMBOL(kit ZS_SHADOWDANCER)
    END

    OUTER_SET has_project_javelin = MOD_IS_INSTALLED "cd_project_javelin.tp2" "1"
    ACTION_IF has_project_javelin BEGIN
        PRINT "Project Javelin detected."
    END

    OUTER_SET has_fg_arms_itm_updates = MOD_IS_INSTALLED "forgotten-armament.tp2" "21"
    ACTION_IF has_fg_arms_itm_updates BEGIN
        PRINT "Forgotten Armament item updates detected."
    END

    OUTER_SET has_scs_spell_nerf = MOD_IS_INSTALLED "setup-stratagems.tp2" "2060"
    ACTION_IF has_scs_spell_nerf BEGIN
        PRINT "SCS spell nerf component detected."
    END

    OUTER_SET has_zs_item_pack_kukris = FILE_EXISTS_IN_GAME ~ZSIPKUK0.ITM~
    ACTION_IF has_zs_item_pack_kukris BEGIN
        PRINT "ZS_ItemPack pack with kukris detected."
    END

    OUTER_SET has_zs_item_pack_estocs = FILE_EXISTS_IN_GAME ~ZSIPEST0.ITM~
    ACTION_IF has_zs_item_pack_estocs BEGIN
        PRINT "ZS_ItemPack pack with estocs detected."
    END

    OUTER_SET has_zs_item_pack_rapiers = FILE_EXISTS_IN_GAME ~ZSIPRAP0.ITM~
    ACTION_IF has_zs_item_pack_rapiers BEGIN
        PRINT "ZS_ItemPack pack with rapiers detected."
    END

    OUTER_SET zs_preinstall_check = 1
END
