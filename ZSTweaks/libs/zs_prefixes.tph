ACTION_IF !VARIABLE_IS_SET ~zs_prefixes~ BEGIN
    ACTION_IF zst_gender_fix BEGIN
        WITH_SCOPE BEGIN
            PRINT "[[FIX]]: Setting all golem, statue, treant, angelic, doppleganger, fungal, beholder, elemental, genie, shambling mound, slime, mist, floating weapon, demonic, mimic, most spectral undead, and mind flayer enemies as niether-gendered if they're set to male or female so it doesn't intrude with opcodes that target male/female"

            OUTER_SET golem = IDS_OF_SYMBOL(race GOLEM)
            OUTER_SET treant = IDS_OF_SYMBOL(race TREANT)
            OUTER_SET statue = IDS_OF_SYMBOL(race STATUE)
            OUTER_SET demon = IDS_OF_SYMBOL(race DEMONIC)
            OUTER_SET mimic = IDS_OF_SYMBOL(race MIMIC)
            OUTER_SET mind_flayer = IDS_OF_SYMBOL(race MIND_FLAYER)
            OUTER_SET darkplanatar = IDS_OF_SYMBOL(race DARKPLANATAR)
            OUTER_SET planatar = IDS_OF_SYMBOL(race PLANATAR)
            OUTER_SET solar = IDS_OF_SYMBOL(race SOLAR)
            OUTER_SET antisolar = IDS_OF_SYMBOL(race ANTISOLAR)
            OUTER_SET shadow = IDS_OF_SYMBOL(race SHADOW)
            OUTER_SET wraith = IDS_OF_SYMBOL(race WRAITH)
            OUTER_SET specter = IDS_OF_SYMBOL(race SPECTER)
            OUTER_SET spectral_undead = IDS_OF_SYMBOL(race SPECTRAL_UNDEAD)
            OUTER_SET doppleganger = IDS_OF_SYMBOL(race DOPPLEGANGER)
            OUTER_SET myconid = IDS_OF_SYMBOL(race MYCONID)
            OUTER_SET elemental = IDS_OF_SYMBOL(race ELEMENTAL)
            OUTER_SET beholder = IDS_OF_SYMBOL(race BEHOLDER)
            OUTER_SET genie = IDS_OF_SYMBOL(race GENIE)
            OUTER_SET shambling_mound = IDS_OF_SYMBOL(race SHAMBLING_MOUND)
            OUTER_SET slime = IDS_OF_SYMBOL(race SLIME)
            OUTER_SET mist = IDS_OF_SYMBOL(race MIST)
            OUTER_SET shrieker = IDS_OF_SYMBOL(race SHRIEKER)
            OUTER_SET ~will-o-wisp~ = IDS_OF_SYMBOL(race "WILL-O-WISP")
            OUTER_SET male = IDS_OF_SYMBOL(gender MALE)
            OUTER_SET female = IDS_OF_SYMBOL(gender FEMALE)
            OUTER_SET niether = IDS_OF_SYMBOL(gender NIETHER)
            OUTER_SET weapon = IDS_OF_SYMBOL(general WEAPON)

        // should it include otyugh?
            COPY_EXISTING_REGEXP ~^.+\.cre$~ ~override~
                SET race = BYTE_AT(0x272)
                SET general = BYTE_AT(0x271)
                PATCH_IF race    = golem              OR
                        race    = treant             OR
                        race    = statue             OR
                        race    = demon              OR
                        race    = mind_flayer        OR
                        race    = mist               OR
                        race    = planatar           OR
                        race    = darkplanatar       OR
                        race    = solar              OR
                        race    = antisolar          OR
                        race    = beholder           OR
                        race    = genie              OR
                        race    = slime              OR
                        race    = mimic              OR
                        race    = doppleganger       OR
                        race    = myconid            OR
                        race    = shadow             OR
                        race    = wraith             OR
                        race    = specter            OR
                        race    = spectral_undead    OR
                        race    = elemental          OR
                        race    = shambling_mound    OR
                        race    = shrieker           OR
                        race    = ~%will-o-wisp%~    OR
                        general = weapon
                BEGIN
                    SET gender = BYTE_AT(0x275)
                    PATCH_IF gender = male OR gender = female BEGIN
                        WRITE_BYTE 0x275 niether
                    END
                END
            BUT_ONLY
        END
    END

    ACTION_IF is_iwdee BEGIN
        ACTION_IF NOT MOD_IS_INSTALLED "EET Fixpack" BEGIN
            PRINT ~[[FIX]]: Fixing broken BONECIR.spl~
            COPY_EXISTING ~#BONECIR.spl~ ~override~
                WRITE_ASCII 0x0 "SPL " (4)
        END
    END

    ACTION_IF is_bg2ee_or_eet BEGIN

        PRINT ~[[FIX]]: Non-standard scimitar/wakizashi/ninjato animations~
        COPY_EXISTING ~misc9q.itm~ ~override~
            WRITE_ASCII 0x22 "SC" #2

        COPY_EXISTING ~sw1h66.itm~ ~override~
            WRITE_ASCII 0x22 "SS" #2

        COPY_EXISTING ~sw1h67.itm~ ~override~
            WRITE_ASCII 0x22 "S1" #2

        COPY_EXISTING ~sw1h68a.itm~ ~override~
            WRITE_ASCII 0x22 "SC" #2

        PRINT "[[FIX]]: Misaligned index in hamm06.itm (Dwarven Thrower +2)"
        COPY_EXISTING ~hamm06.itm~ ~override~ // Dwarven Thrower +3
            LPF FJ_SPL_ITM_REINDEX END

        PRINT "[[FIX]]: Aesgareth enemies he fights during the card game being considered 'weapon' instead of 'humanoid'"
        COPY_EXISTING ~gordeck1.cre~ ~override~
                      ~gordeck2.cre~ ~override~
                      ~gordeck3.cre~ ~override~
                      ~gordeck4.cre~ ~override~
            PATCH_IF BYTE_AT(0x271) = 101 BEGIN
                WRITE_BYTE 0x271 1
            END
    END

    ACTION_IF is_bgee_bg2ee_or_eet BEGIN
        PRINT "[[FIX]]: Removing bizarre & unused spell selection from shadow creatures that can be exploitable by the player"
    END

    ACTION_IF is_bgee_or_eet BEGIN

        COPY_EXISTING   ~bdwrai01.cre~ ~override~ // wraith
                        ~bdwrait1.cre~ ~override~ // wraith
                        ~bpshadow.cre~ ~override~ // shadow
                        ~bpwrai01.cre~ ~override~ // wraith
                        ~daitel.cre~   ~override~ // ghost
                        ~mistpo01.cre~ ~override~ // poison mist
                        ~mistposu.cre~ ~override~ // poison mist
                        ~shadfi02.cre~ ~override~ // devil shade
                        ~shadowsu.cre~ ~override~ // shadow
                        ~slaysh01.cre~ ~override~ // slayer shadow
            REMOVE_MEMORIZED_SPELL ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
            REMOVE_KNOWN_SPELL     ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
            BUT_ONLY IF_EXISTS

    END ELSE ACTION_IF is_bg2ee_or_eet BEGIN // bg2ee

            COPY_EXISTING   ~chwraith.cre~ ~override~ // child spirit
                            ~firwra01.cre~ ~override~ // greater wraith
                            ~fsghos.cre~   ~override~ // ghost
                            ~fsghos2.cre~  ~override~ // ghost
                            ~gormisti.cre~ ~override~ // ice mist
                            ~gormistp.cre~ ~override~ // poison mist
                            ~hgmis01.cre~  ~override~ // vampiric wraith
                            ~hgmis02.cre~  ~override~ // swamp horror
                            ~hgwra01.cre~  ~override~ // gorion (casts all spells via forcespell)
                            ~hgwra02.cre~  ~override~ // devil shade
                            ~hgwra03.cre~  ~override~ // devil shade
                            ~kshadow.cre~  ~override~ // shadow
                            ~mistcd01.cre~ ~override~ // crimson death
                            ~mistho01.cre~ ~override~ // mist horror
                            ~mistpo01.cre~ ~override~ // poison mist
                            ~mistposu.cre~ ~override~ // poison mist
                            ~mistva01.cre~ ~override~ // vampiric mist
                            ~mistwa01.cre~ ~override~ // wandering horror
                            ~ohhburic.cre~ ~override~ // burich
                            ~ohhshad.cre~  ~override~ // shadow
                            ~ohhshfi1.cre~ ~override~ // shadow fiend
                            ~ohhshfi2.cre~ ~override~ // devil shade
                            ~rngsha.cre~   ~override~ // shadow
                            ~rngsha01.cre~ ~override~ // shadow
                            ~rngsha02.cre~ ~override~ // shadow patrick - knows cure medium wounds and has generic priest script to use it, but doesn't have it memorized
                            ~rngsha03.cre~ ~override~ // shadow
                            ~rngsha04.cre~ ~override~ // shadow jailor
                            ~rngsha2d.cre~ ~override~ // shadow patrick
                            ~sarspir.cre~  ~override~ // prison spirit
                            ~sdshadfi.cre~ ~override~ // shadow fiend
                            ~sdshadow.cre~ ~override~ // shadow
                            ~sewsha01.cre~ ~override~ // shadow fiend
                            ~sewsha02.cre~ ~override~ // shadow fiend
                            ~sewsha03.cre~ ~override~ // shadow
                            ~shadfi01.cre~ ~override~ // shadow fiend
                            ~shadfi02.cre~ ~override~ // devil shade
                            ~shadow01.cre~ ~override~ // shadow
                            ~shadowsu.cre~ ~override~ // shadow
                            ~slaysh01.cre~ ~override~ // slayer shadow
                            ~spectr01.cre~ ~override~ // specter
                            ~spellh01.cre~ ~override~ // spellhaunt
                            //~tanwiz1.cre~  ~override~ // vellin dahn
                            ~telslav.cre~  ~override~ // slave wraith
                            ~telwrai.cre~  ~override~ // demon wraith
                            ~uhcreat.cre~  ~override~ // shadow
                            ~wraith01.cre~ ~override~ // wraith
                            ~wrashi01.cre~ ~override~ // shimmering wraith
                REMOVE_MEMORIZED_SPELL ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
                REMOVE_KNOWN_SPELL ~spwi112~ ~spwi212~ ~spwi304~ ~spwi306~ ~spwi308~ ~spwi402~ ~spwi415~ ~spwi501~ ~spwi502~ ~spwi504~ ~spwi505~ ~spwi508~ ~spwi601~ ~spwi602~ ~spwi604~
            BUT_ONLY IF_EXISTS
        END

    OUTER_SET ~zs_prefixes~ = 1
END
